//Bk //rik
//xml tools
external function string 255 ConvertXml(string);
//txt tools
external procedure ExtractObjWithSeparator(string,string,Boolean,var Integer,var string);
external function string 255 AddStr(string,string,string);

remote procedure DoSendAddrLookupRIKVer6(string,string,var area,var boolean,var boolean);
remote function boolean EServiceActivated(string); //Bk KB 30.08.2017
//Bk - start //eteenused ts - 2018/04, e-service statistic
remote updating procedure StoreCUServiceUsage(date,string,Integer,string,Integer,Integer);
//Bk - end

function string 255 cutString(string namestr)
begin
  string 255 cutlist,cutlist2,newnamestr;

  newnamestr = namestr;
  cutlist = "AS,O†,SA,K†"; //was sstring, but replaced with hardcoded, as company are not translated usually
  cutlist2 = "MT†";
  if (SetInSet(right(namestr,2),cutlist)) then begin
    newnamestr = Left(namestr,(len(namestr)-3));
  end;
  if (SetInSet(right(namestr,3),cutlist2)) then begin
    newnamestr = Left(namestr,(len(namestr)-4));
  end;
  cutString = newnamestr;
  return;
end;

function boolean RequestAddrForRegAndNameRIKVer6(string regno,string namestr,var area reply)
begin
  Boolean res,noResult;
  string 255 newnamestr;
  record CYBlock CYb;
  Integer cnt;
  
  newnamestr = Uppercase(namestr);
  DoSendAddrLookupRIKVer6(regno,ConvertXml(newnamestr),reply,noResult,res);
  if (noResult) then begin
    newnamestr = cutString(Uppercase(namestr));
    if (newnamestr != namestr) then begin
      DoSendAddrLookupRIKVer6(regno,ConvertXml(newnamestr),reply,noResult,res);
    end;
  end;
  if (res) then begin
    BlockLoad(CYb);
    cnt = 1;
    if (noResult) then begin cnt = 0; end;
    queued.StoreCUServiceUsage(CurrentDate,CYb.OrgNr,5,"ESTADRLOOKUP",1,cnt);//ts - 2018/04 , e-service statistic
  end;
  RequestAddrForRegAndNameRIKVer6 = res;
  return;
end;

global
procedure EstAddrLookupRn(record RcVc RepSpec)
begin
  xml xmlReply;
  Boolean res,noResult;
  string 255 xkey,subxkey,tstr,name,regnr,vatnr,addr1,addr2,addr3,status,link;
  array string 255 aCUInf;
  Integer i,aCnt,rw;
  Area a_reply;

  res = RequestAddrForRegAndNameRIKVer6(RepSpec.f2,RepSpec.f3,a_reply);
  rw = 1;
  StartReportJob(USetStr(1500107));
  if (nonblank(RepSpec.f2)) then begin 
    tstr = USetStr(1500109) & ": " & RepSpec.f2;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  if (nonblank(RepSpec.f3)) then begin 
    tstr = USetStr(1500108) & ": " & RepSpec.f3;
    Header(rw,tstr,1);
    rw = rw + 1;
  end;
  EndHeader;

  xmlReply = ParseXMLArea(a_reply);

  aCnt = 0;
  SetRepCol(2,110);
  SetRepCol(3,152);
  SetRepCol(4,209);
  SetRepCol(5,275);
  
  if (res == false) then begin
    goto LEstAddrLookupRn;
  end;
  
  if (XmlNodeExists(xmlReply,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring")) then begin
    tstr = XmlGet(xmlReply,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring");
    StartFormat(15);
     OutString(0,0,tstr,false);
    EndFormat;
    goto LEstAddrLookupRn;
  end;

  xkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/ns1:ettevotjaRekvisiidid_v1Response/ns1:keha/ns1:ettevotjad/";
  for (i=0; XmlNodeExists(xmlReply,xkey & "ns1:Item[" & i & "]"); i=i+1) begin
    subxkey = xkey & "ns1:Item[" & i & "]/";
    name = XmlGet(xmlReply,subxkey & "ns1:nimi");
    regnr = XmlGet(xmlReply,subxkey & "ns1:ariregistri_kood");
    vatnr = XmlGet(xmlReply,subxkey & "ns1:kmkr_nr");
    tstr = XmlGet(xmlReply,subxkey & "ns1:ettevotja_staatus");
    status = XmlGet(xmlReply,subxkey & "ns1:ettevotja_staatus_tekstina");
    addr1 = XmlGet(xmlReply,subxkey & "ns1:ettevotja_aadress/ns1:asukoht_ettevotja_aadressis");
    addr2 = XmlGet(xmlReply,subxkey & "ns1:ettevotja_aadress/ns1:asukoha_ehak_tekstina");
    addr3 = XmlGet(xmlReply,subxkey & "ns1:ettevotja_aadress/ns1:indeks_ettevotja_aadressis");
    link = XmlGet(xmlReply,subxkey & "ns1:teabesysteemi_link");
    ACUInf[aCnt] = RepSpec.ArtMode & ";" & name & ";" & regnr & ";" & vatnr & ";" & addr1 & ";" & addr2 & ";" & addr3;
    if (aCnt == 0) then begin
      StartFormat(15);
      OutString(0,0,USetStr(1500108),false);
      OutString(2,0,USetStr(1500109),false);
      OutString(3,0,USetStr(1500110),false);
      OutString(4,0,USetStr(1500111),false);
      OutString(5,0,USetStr(1500112),false);
      EndFormat;
    end;
    Gray_Divider(0,1);
    StartFormat(15);
    OutString(0,0,name,false);
    OutString(2,0,regnr,false);
    OutString(3,0,vatnr,false);
    OutString(4,0,status,false);
    OutString(5,0,addr1,false);
    if (RepSpec.ArtMode > 0) then begin
      if (RepSpec.flags[1] == 1) then begin
        OutStringID(1,"DblAddrUpdCU",USetStr(1500122),true,ACUInf[aCnt]);
        EndFormat;
        StartFormat(15);
        OutString(5,0,addr2,false);
        OutStringID(1,"DblAddrCreateCU",USetStr(1500123),true,ACUInf[aCnt]);
        EndFormat;
        StartFormat(15);
        OutString(5,0,addr3,false);
      end else begin
        OutStringID(1,"DblAddrUpdCU",USetStr(1500114),true,ACUInf[aCnt]);
        EndFormat;
        StartFormat(15);
        OutString(5,0,addr2,false);
        OutStringID(1,"DblAddrUpdCUName",USetStr(1500121),true,ACUInf[aCnt]);
        EndFormat;
        StartFormat(15);
        OutString(5,0,addr3,false);
        OutStringID(1,"DblAddrCreateCU",USetStr(1500115),true,ACUInf[aCnt]);
        EndFormat;
        StartFormat(15);
      end;
      OutStringID(1,"DblOpenCUInfoInWeb",USetStr(1500116),true,link);
      EndFormat;
    end else begin
      OutStringID(1,"DblAddrCreateCU",USetStr(1500115),true,ACUInf[aCnt]);
      EndFormat;
      StartFormat(15);
      OutString(5,0,addr2,false);
      OutStringID(1,"DblOpenCUInfoInWeb",USetStr(1500116),true,link);
      EndFormat;
      if (nonblank(addr3)) then begin
        StartFormat(15);
        OutString(5,0,addr3,false);
        EndFormat;
      end;
    end;
    aCnt = aCnt + 1; 
  end;
  Black_Divider(0,1);
  StartFormat(15);
  if (aCnt >= 100) then begin
    OutString(6,0,USetStr(1500117),false);
  end else begin
    OutString(6,0,USetStr(1500118) & aCnt,false);
  end;
  EndFormat;  
LEstAddrLookupRn:;
  if (aCnt==0) then begin
    StartFormat(15);
    OutString(0,0,USetStr(1500113),false);
    EndFormat;
  end;
  if (res==false) then begin
    StartFormat(15);
    OutString(0,0,USetStr(1500124),false);
    EndFormat;
  end;
  EndJob;
  return;
end;

global
procedure SendEstAddressLookup(Integer wn,string regno,string namestr,Integer stat)
begin
  record RcVc RepSpec;
  
  //if (EServiceActivated("SENDESTADRLOOKUP")) then begin
  ReportDefaults(RepSpec,"EstAddrLookupRClass");
  RepSpec.repname = "EstAddrLookupRn";
  /* //ts - 2018/06
  end else begin
    MessageBox(1500005,"");
    goto LSendEstAddressLookup;
  end;
  */
  RepSpec.Media = mtScreen;
  RepSpec.f2 = regno;
  RepSpec.f3 = namestr;
  RepSpec.flags[0] = 0;
  RepSpec.flags[1] = stat;
  RepSpec.ArtMode = wn;
  RunReport(RepSpec,0);
LSendEstAddressLookup:;
  return;
end;

global
function Boolean EstAddrLookupRClassOnOKWindow(Integer wn)
begin
  record RcVc RepSpec,OldRepSpec;

//  if (EServiceActivated("SENDESTADRLOOKUP")) then begin //Bk JL 190919
    DeselectWindow(wn,true);
    GetWindowRecord(wn,OldRepSpec);
    ReportDefaults(RepSpec,"EstAddrLookupRClass");    
    RepSpec.repname = "EstAddrLookupRn";
    RepSpec.Media = mtScreen;
    RepSpec.f2 = OldRepSpec.f2;
    RepSpec.f3 = OldRepSpec.f3;
    RepSpec.flags[0] = 0;
    RepSpec.flags[1] = OldRepSpec.flags[1];
    RepSpec.ArtMode = OldRepSpec.ArtMode;
    RunReport(RepSpec,0);
//Bk start JL 190919
/*  end else begin 
    MessageBox(1500005,"");
  end;*/
//Bk end
  CloseWindow(wn);
  EstAddrLookupRClassOnOKWindow = true;
  return;
end;

procedure FormatCUAddrRows(var record CUVc CUr)
begin
  //shift empty lines to top
  if(blank(CUr.InvAddr0)) then begin
    CUr.InvAddr0 = CUr.InvAddr1;
    CUr.InvAddr1 = CUr.InvAddr2;
    CUr.InvAddr2 = CUr.InvAddr3;
    CUr.InvAddr3 = "";
  end;
  if(blank(CUr.InvAddr1)) then begin
    CUr.InvAddr1 = CUr.InvAddr2;
    CUr.InvAddr2 = CUr.InvAddr3;
    CUr.InvAddr3 = "";
  end;
  if(blank(CUr.InvAddr2)) then begin
    CUr.InvAddr2 = CUr.InvAddr3;
    CUr.InvAddr3 = "";
  end;
  return;
end;

updating procedure UpdCustomer(string CUCode,string tstr)
begin
  record CUVc CUr,oldCUr;
  string 255 res;
  string 255 name,regno,vatno,addr1,addr2,addr3,wnstr,country;
  Integer pos;

  CUr.Code = CUCode;
  if (ReadFirstMain(CUr,1,true)) begin
    RecordCopy(oldCUr,CUr);
    pos = 0;
    ExtractObjWithSeparator(";",tstr,true,pos,wnstr);
    ExtractObjWithSeparator(";",tstr,true,pos,name);
    ExtractObjWithSeparator(";",tstr,true,pos,regno);
    ExtractObjWithSeparator(";",tstr,true,pos,vatno);
    ExtractObjWithSeparator(";",tstr,true,pos,addr1);
    ExtractObjWithSeparator(";",tstr,true,pos,addr2);
    ExtractObjWithSeparator(";",tstr,true,pos,addr3);
    ExtractObjWithSeparator(";",tstr,true,pos,country);
    CUr.Name = URLDecode(name);
    CUr.VATNr = vatno;
    CUr.InvAddr0 = URLDecode(addr1);
    CUr.InvAddr1 = URLDecode(addr2);
    CUr.InvAddr2 = URLDecode(addr3);
    CUr.InvAddr3 = URLDecode(country);
    FormatCUAddrRows(CUr);
    if (RecordUpdate(oldCUr,CUr,false)==0) then begin
    end;
  end;
  return;
end;

global
procedure DblAddrUpdCU(string dblstr,string l,Integer currepwn)
begin
  record CUVc CUr;
  record RcVc RepSpec;
  string 255 tstr,res;
  string 255 name,regno,vatno,addr1,addr2,addr3,wnstr,country;
  Integer wn,nwn;
  Integer pos;

  tstr = l;
  pos = 0;
  ExtractObjWithSeparator(";",tstr,false,pos,wnstr);
  wn = StringToInt(wnstr);
  if (wn > 0 AND GetWindowClass(wn) == "CUDClass") then begin //Bk fix KB 07.09.2017
    DeselectWindow(wn,false); //Bk KB 31.08.2017 - without it in some cases not all fields got updated
    GetWindowRecord(wn,CUr);
    ExtractObjWithSeparator(";",tstr,false,pos,name);
    ExtractObjWithSeparator(";",tstr,false,pos,regno);
    ExtractObjWithSeparator(";",tstr,false,pos,vatno);
    ExtractObjWithSeparator(";",tstr,false,pos,addr1);
    ExtractObjWithSeparator(";",tstr,false,pos,addr2);
    ExtractObjWithSeparator(";",tstr,false,pos,addr3);
    ExtractObjWithSeparator(";",tstr,false,pos,country);
    if (blank(CUr.Name)) then begin
      CUr.Name = URLDecode(name);
    end;
    CUr.RegNr1 = regno;
    CUr.VATNr = vatno;
    if(nonblank(addr1) OR nonblank(addr2) OR nonblank(addr3) OR nonblank(country)) then begin
      CUr.InvAddr0 = URLDecode(addr1);
      CUr.InvAddr1 = URLDecode(addr2);
      CUr.InvAddr2 = URLDecode(addr3);
      CUr.InvAddr3 = URLDecode(country);
      FormatCUAddrRows(CUr);
    end;
    PutWindowRecord(wn,CUr);
  end;
  return;
end;

global
procedure DblAddrUpdCUName(string dblstr,string l,Integer currepwn)
begin
  record CUVc CUr;
  record RcVc RepSpec;
  string 255 tstr,res;
  string 255 name,regno,vatno,addr1,addr2,addr3,wnstr;
  Integer wn,nwn;
  Integer pos;

  tstr = l;
  pos = 0;
  ExtractObjWithSeparator(";",tstr,false,pos,wnstr);
  wn = StringToInt(wnstr);
  if (wn > 0 AND GetWindowClass(wn) == "CUDClass") then begin //Bk fix KB 07.09.2017
    DeselectWindow(wn,false); //Bk KB 31.08.2017
    GetWindowRecord(wn,CUr);
    ExtractObjWithSeparator(";",tstr,false,pos,name);
    CUr.Name = URLDecode(name);
    PutWindowRecord(wn,CUr);
  end;
  return;
end;

global
procedure DblAddrCreateCU(string dblstr,string l,Integer currepwn)
begin
  record CUVc CUr,lCUr;
  record RcVc RepSpec;
  string 255 tstr,res;
  string 255 name,regno,vatno,addr1,addr2,addr3,wnstr,country;
  Integer wn,nwn;
  Integer pos;

  tstr = l;
  pos = 0;
  ExtractObjWithSeparator(";",tstr,false,pos,wnstr);
  ExtractObjWithSeparator(";",tstr,false,pos,name);
  ExtractObjWithSeparator(";",tstr,false,pos,regno);
  ExtractObjWithSeparator(";",tstr,false,pos,vatno);
  ExtractObjWithSeparator(";",tstr,false,pos,addr1);
  ExtractObjWithSeparator(";",tstr,false,pos,addr2);
  ExtractObjWithSeparator(";",tstr,false,pos,addr3);
  ExtractObjWithSeparator(";",tstr,false,pos,country);
  lCUr.RegNr1 = regno;
  if (ReadFirstKey("RegNr1",lCUr,1,true)) then begin
    nwn = OpenWindow("UpdEstAddrVcCancelDsmYesNoWClass",0,0,"","",RepSpec);
    DeselectWindow(nwn,false);
    RepSpec.f1 = lCUr.Code;
    RepSpec.f2 = tstr;
    PutWindowRecord(nwn,RepSpec);
  end else begin
    RecordNew(CUr);
    CUr.Name = URLDecode(name);
    CUr.RegNr1 = regno;
    CUr.VATNr = vatno;
    CUr.InvAddr0 = URLDecode(addr1);
    CUr.InvAddr1 = URLDecode(addr2);
    CUr.InvAddr2 = URLDecode(addr3);
    CUr.InvAddr3 = URLDecode(country);
    FormatCUAddrRows(CUr);
    CUr.CUType = 1;
    CUr.VEType = 1;
    nwn = OpenWindow("CUDClass",1,0,"","",CUr);
  end;
  return;
end;

global
procedure DblOpenCUInfoInWeb(string dblstr,string l,Integer currepwn)
begin
  string 255 link;
  string 255 element;
  
  if (nonblank(l)) then begin
    link = l;
  end;
  if (nonblank(link)) then begin
    OpenWebBrowser(link);
  end;
  return;
end;

global
updating procedure UpdEstAddrVcCancelDsmYesNoWClassYES()
begin
  Integer wn;
  record RcVc RepSpec;

  wn = CurWindow;
  GetWindowRecord(wn,RepSpec);
  CloseWindow(wn);      
  UpdCustomer(RepSpec.f1,RepSpec.f2);
  return;
end;

global
procedure UpdEstAddrVcCancelDsmYesNoWClassNO()
begin
  Integer wn;

  wn = CurWindow;
  CloseWindow(wn);      
  return;
end;
