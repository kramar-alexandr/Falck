//pangaliidestus
external updating procedure PaymPayrollXMLEn(record RcVc);
remote updating procedure StoreCUServiceUsage(date,string,Integer,string,Integer,Integer);
external function string 255 GetUniqueFileNameForPaymPayroll(record PaymPayrollVc);
external function integer BankReqTypeToHTSServNumber(string);
remote updating function boolean PaymPayrollAddToBankReqQueue(record PaymPayrollVc,area,string,var record BankReqQueVc);
remote function boolean PaymPayrollCanAddToBankQueue(record PaymPayrollVc,var string);
remote procedure SendBdocToBankHTS(area,string,var record BankReqQueVc,var string);
remote procedure BankingDebugFile(area,string);
external function integer BankQueReplyProgress();
external function integer BankQueReplySuccess();
external function integer BankQueReplyError();
remote function boolean IsBankQueueActive();
external updating function boolean SignAreaWithIDCard(area,string,string,var area,var string,var string); //must not be remote

updating
procedure ExportPaymPayrollSEPAForSign(record PaymPayrollVc PaymPayrollr, var string fileName, var area XMLPaymPayrollArea)
begin
  record RcVc RepSpec;
  string 20 tmpFolder;
  record OPTBlock OPTb;
  record LocalMachineBlock LMb;
  
  BlockLoad(OPTb);
  BlockLoad(LMb);
  tmpFolder = "tmp/";
  fileName = GetUniqueFileNameForPaymPayroll(PaymPayrollr) & ".xml";
  if (OpenExportFile(tmpFolder & filename,false) == false) then begin
    MessageBox(1695," " & filename);
  end;
  ReportDefaults(RepSpec,"PaymPayrollXMLEClass");
  RepSpec.sStartDate = PaymPayrollr.PaymDate;
  RepSpec.sEndDate = PaymPayrollr.PaymDate;
  RepSpec.f1 = PaymPayrollr.SerNr;
  RepSpec.repname = "PaymPayrollXMLEn";
  RepSpec.Media = mtFile;

  RepSpec.OnlyUnprntd = 1;
  RepSpec.ArtMode = OPTb.DOSFlag;
  RepSpec.flags[4] = OPTb.Bankfees;
  RepSpec.flags[0] = OPTb.OnDUEDate;
  RepSpec.f12 = LMb.DefExpCodepage;
  RepSpec.FirstAcc = PaymPayrollr.PMCode;
  PaymPayrollXMLEn(RepSpec);
  CloseFile;
  AddFileToArea(tmpFolder & fileName,XMLPaymPayrollArea,false);
  BankingDebugFile(XMLPaymPayrollArea,PaymPayrollr.SerNr & "_paympayroll_area.xml");
  Delete_File(tmpFolder & fileName);
  return;
end;

global updating
procedure ExcPINWClassOKPaymPayroll(record RcVc RepSpec)
begin
  record PaymPayrollVc PaymPayrollr;
  string 5 pin;
  area xmlFileArea,signedArea;
  string 255 errMsg,fileName,signedFileName;
  record BankReqQueVc BankReqQuer;

  pin = RepSpec.f1;
  PaymPayrollr.SerNr = RepSpec.long1;
  if (ReadFirstMain(PaymPayrollr,1,true)) then begin
    ExportPaymPayrollSEPAForSign(PaymPayrollr,fileName,xmlFileArea);
    if (SignAreaWithIDCard(xmlFileArea,fileName,pin,signedArea,signedFileName,errMsg)) then begin
      if (PaymPayrollAddToBankReqQueue(PaymPayrollr,xmlFileArea,signedFileName,BankReqQuer)) then begin
        threadremoteasync.SendBdocToBankHTS(signedArea,signedFileName,BankReqQuer,errMsg); //run from non main thread, as involves web request
        MessageBox(1501086,"");
        StoreCUServiceUsage(CurrentDate,BankReqQuer.RegNr,BankReqTypeToHTSServNumber(BankReqQuer.BankReqType),BankReqQuer.Partner,1,1);
      end else begin
        MessageBox(23192,": " & USetStr(1501087));
      end;
    end else begin
      MessageBox(1501088," " & errMsg);
    end;
  end else begin
    MessageBox(28263,": " & PaymPayrollr.SerNr);
  end;
  return;
end;

global updating
procedure ExcPINWClassNoSignPaymPayroll(record RcVc RepSpec)
begin
  record PaymPayrollVc PaymPayrollr;
  area xmlFileArea;
  string 255 errMsg,fileName;
  record BankReqQueVc BankReqQuer;

  PaymPayrollr.SerNr = RepSpec.long1;
  if (ReadFirstMain(PaymPayrollr,1,true)) then begin
    ExportPaymPayrollSEPAForSign(PaymPayrollr,fileName,xmlFileArea);
    if (PaymPayrollAddToBankReqQueue(PaymPayrollr,xmlFileArea,fileName,BankReqQuer)) then begin
      threadremoteasync.SendBdocToBankHTS(xmlFileArea,fileName,BankReqQuer,errMsg); //run from non main thread, as involves web request
      MessageBox(1501086,"");
      StoreCUServiceUsage(CurrentDate,BankReqQuer.RegNr,BankReqTypeToHTSServNumber(BankReqQuer.BankReqType),BankReqQuer.Partner,1,1);
    end else begin
      MessageBox(23192,": " & USetStr(1501087));
    end;
  end else begin
    MessageBox(28263,": " & PaymPayrollr.SerNr);
  end;
  return;
end;

procedure RequestEstIDCardPin2ForPaymPayroll(record PaymPayrollVc PaymPayrollr)
begin
  record RcVc RepSpec;

  RepSpec.long1 = PaymPayrollr.SerNr;
  RepSpec.f4 = "PaymPayrollVc";
  OpenWindow("ExcPINWClass",0,CurWindow,"","",RepSpec);
  return;
end;

global
updating procedure SignAndSendPPayrlDsm()
begin
  integer wn;
  record PaymPayrollVc PaymPayrollr;
  record RcVc RepSpec;
  area xmlFileArea;
  string 255 errMsg,fileName;

  wn = CurWindow;
  if (wn > 0) then begin
    if (GetWindowClass(wn)=="PaymPayrollDClass") then begin
      GetWindowRecord(wn,PaymPayrollr);
      if (PaymPayrollCanAddToBankQueue(PaymPayrollr,errMsg)) then begin
        RequestEstIDCardPin2ForPaymPayroll(PaymPayrollr);
      end else begin
        MessageBox(0,errMsg);
        if (IsBankQueueActive == false) then begin
          OpenWindow("BankInfoWClass",0,CurWindow,"","",RepSpec);
        end;
      end;
    end;
  end;
  return;
end;

global
procedure BK_StatusPayPrlDsm()
begin
  Integer wn;
  record PaymPayrollVc PaymPayrollr;
  record RcVc RepSpec;
  
  if(IsBankQueueActive == false) then begin
    OpenWindow("BankInfoWClass",0,CurWindow,"","",RepSpec);
  end else begin
    wn = CurWindow;
    GetWindowRecord(wn,PaymPayrollr);
    ReportDefaults(RepSpec,"EPayStatRClass"); //reusing this window
    RepSpec.f1 = PaymPayrollr.SerNr;
    RepSpec.Media = mtScreen;
    RepSpec.repname = "BK_EPaymPayrollStatRn";
    RunReport(RepSpec,0);
  end;
  return;
end;

//this function is missing in std, only in Books HAL yet
global
function integer PaymPayrollLClassRowColor(record PaymPayrollVc PaymPayrollr)
begin
  integer res;
  record BankReqQueVc BankReqQuer;
  
  res = kRowColorNone;
  BankReqQuer.RecordType = "PaymPayrollVc";
  BankReqQuer.RecordID = PaymPayrollr.SerNr & "";
  //find in header
  if (ReadFirstKey("RecordIDType",BankReqQuer,2,true)) then begin
    if (BankReqQuer.ReplyStatCode == BankQueReplySuccess) begin
      res = kRowColorGreen;
      goto LPaymPayrollLClassRowColor;
    end;
    if (BankReqQuer.ReplyStatCode == BankQueReplyProgress) begin
      res = kRowColorYellow;
      goto LPaymPayrollLClassRowColor;
    end;
    if (BankReqQuer.ReplyStatCode == BankQueReplyError) begin
      res = kRowColorRed;
      goto LPaymPayrollLClassRowColor;
    end;
  end else begin
  //find in matrix
    ResetLoop(BankReqQuer);
    RecordClear(BankReqQuer);
    BankReqQuer.RecordType = "PaymPayrollVc";
    if (ReadFirstKey("RowRecordIDType:" & PaymPayrollr.SerNr,BankReqQuer,1,true)) then begin
      if (BankReqQuer.ReplyStatCode == BankQueReplySuccess) begin
        res = kRowColorGreen;
        goto LPaymPayrollLClassRowColor;
      end;
      if (BankReqQuer.ReplyStatCode == BankQueReplyProgress) begin
        res = kRowColorYellow;
        goto LPaymPayrollLClassRowColor;
      end;
      if (BankReqQuer.ReplyStatCode == BankQueReplyError) begin
        res = kRowColorRed;
        goto LPaymPayrollLClassRowColor;
      end;
    end;
  end;
LPaymPayrollLClassRowColor:;
  PaymPayrollLClassRowColor = res;
  return;
end;
