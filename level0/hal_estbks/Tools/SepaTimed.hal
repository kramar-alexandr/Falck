//Bk //sepa import
external updating procedure SEPAInXML(xml,var string);

updating 
procedure SepaScanImportFolder()
begin
  record SEPABankFileBlock SEPABankFileBl;
  xml xdata;
  Boolean testf;
  string 255 xstr,newfilename,newfullname;
  string 255 foldername,filename,fullname,tofolder,errfolder;
  Integer err,res;
  string 5 extension,separator;
  Time ctime;
  date cdate;
  string 5 comp,invtype;
  Boolean closef,test;
  string 255 errMsg;
  
  BlockLoad(SEPABankFileBl);

  foldername = SEPABankFileBl.ImportPathFrom;
  
  tofolder = SEPABankFileBl.ImportPathTo; //Destination folder

  extension = "xml";
  separator = "\\";
  filename = FileNameFromFolder(foldername,extension); 
  while (nonblank(filename) and filename<>"..") begin
     res = MoveFile(filename,foldername,tofolder);
     test = false;
     if (res==0)  then begin 
       test = true;
     end; 
     if (test) then begin 
       fullname = tofolder & separator & filename;
       xdata = ParseXMLFile(fullname);
       ctime = CurrentTime;
       cdate = CurrentDate;
       newfilename = DateToString(cdate,"YYMMDD") & GetHour(ctime) & GetMinute(ctime) & "_" & filename;
       newfullname = tofolder & separator & newfilename;
       if (RenameFile(fullname,newfullname)==0) then begin
       end;
       SEPAInXML(xdata,errMsg);
     end;
     filename = FileNameFromFolder(foldername,extension); 
  end;
  return;
end;
 
global
updating procedure SepaTimed2()
begin
  Time t,ltime;
  Date d;
  record SEPABankFileBlock SEPABankFileBl;
  boolean importrun;

  BlockLoad(SEPABankFileBl);
  
  ltime = SEPABankFileBl.LastImportHour;
  if nonblank(SEPABankFileBl.ImportPathFrom) and nonblank(SEPABankFileBl.ImportPathTo) then begin 
    t = CurrentTime;
    d = CurrentDate;
    importrun = false;
    if ((t.hour==SEPABankFileBl.ImportHour) and (t.hour!=ltime.hour) and (d<>SEPABankFileBl.LastImportDate)) then begin
      importrun = true;
    end;  
    if (SEPABankFileBl.TimedAction==1) then begin 
      importrun = true;
    end;
    if (importrun==true) then begin
      SepaScanImportFolder;
      SEPABankFileBl.LastImportDate = d;
      SEPABankFileBl.LastImportHour = t;
      BlockStore(SEPABankFileBl); 
    end;  
  end; 
  return;
end;  

global
updating procedure SepaTimed()
begin
  record CompaniesBlock CompaniesRec;
  row CompaniesBlock comprw;
  Integer i,rwcnt;
  Integer compnr,curcomp;

  if (ServerMode OR SingleUserMode) then begin
    curcomp = CurrentCompany;  
    BlockLoad(CompaniesRec);
    rwcnt = MatRowCnt(CompaniesRec);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(CompaniesRec,i,comprw);
      if (SetCompanyCode(comprw.CompCode,false)) then begin
        SepaTimed2;
      end;
      ResetCompany(curcomp);
    end;
    ResetCompany(curcomp);
  end;
  return;
end; 

