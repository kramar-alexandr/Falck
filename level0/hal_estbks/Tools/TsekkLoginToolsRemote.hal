//Bk //tsekk.ee
external function Integer InString2(string,string);

//new functions only
external procedure SimpleDebugMsg(string, string);
external function string 20 GetTsekkPartner();

function boolean IsTsekkAuthLink(string link)
begin
  boolean res;

  res = false;
  if (InString2(link,"/?access_validation_bind=") != -1) then begin
    res = true;
  end;
  IsTsekkAuthLink = res;
  return;
end;

function boolean IsTsekkLoginLink(string link)
begin
  boolean res;

  res = false;
  if (InString2(link,"/?code=") != -1) then begin
    res = true;
  end;
  IsTsekkLoginLink = res;
  return;
end;

function string 255 GetRedirectLinkFromArea(area areap)
begin
  string 255 res,startText;
  integer linkStartPos;
  area linkArea;

  res = "";
  startText = "Found. Redirecting to ";
  linkStartPos = FindStringInArea(startText,areap);
  if (linkStartPos != -1) then begin
    linkStartPos = linkStartPos + len(startText);
    res = GetStringFromArea(areap,linkStartPos,GetAreaLength(areap)-linkStartPos);
  end;
  GetRedirectLinkFromArea = res;
  return
end;

function boolean SubscrCommTsekk(boolean subscribe)
begin
  boolean res;
  area requestArea,replyArea;
  record ServiceCacheVc SCr;
  record TsekkSetBlock TsekkSetbl;
  string 20 partnerName,action;
  string 50 token;
  string 255 errMsg;
  boolean reqRes;

  BlockLoad(TsekkSetbl);
  token = TsekkSetbl.AccessToken;
  partnerName = GetTsekkPartner;
  if (subscribe) then begin
    action = "subscribe";
  end else begin
    action = "unsubscribe";
  end;
  SCr.Code = "LOGINTOTSEKKEE";
  SCr.Partner = "TSEKKEE";
  if(ReadFirstMain(SCr,2,true)) then begin
    reqRes = SendWebRequest(SCr.ServiceHost,SCr.ServicePort,-1,true,"GET",SCr.FuncName & "/" & partnerName & "/" & action & "/" & token,"charset=utf-8","",false,requestArea,replyArea,10);
    if (GetAreaLength(replyArea) == 0 AND reqRes == true) then begin //first subscribe gives 200 with 0 body
      errMsg = USetStr(1500207);
      res = true;
      goto LSubscrCommTsekk;
    end;
    if (IsTsekkLoginLink(GetRedirectLinkFromArea(replyArea))) then begin //next subscribes give 302 with redirect
      errMsg = USetStr(1500207);
      res = true;
      goto LSubscrCommTsekk;
    end;
    if (IsTsekkAuthLink(GetRedirectLinkFromArea(replyArea))) then begin
      errMsg = USetStr(1500206);
      res = true;
      goto LSubscrCommTsekk;
    end;
    //wrong non existing token gives 500 Internal Server Error and we don't process it as positive res
  end else begin
    SimpleDebugMsg("Costpocket","Login service cache is NOT found.");
  end;
LSubscrCommTsekk:;
  SubscrCommTsekk = res;
  return;
end;

global
function boolean RegisterTokenAtTsekk()
begin
  boolean res;

  res = SubscrCommTsekk(true);
  RegisterTokenAtTsekk = res;
  return;
end;

global
function boolean UnRegisterTokenAtTsekk()
begin
  boolean res;

  res = SubscrCommTsekk(false);
  UnRegisterTokenAtTsekk = res;
  return;
end;

global
procedure GetLoginLinkRemote(string partnerName, string token, var string res)
begin
  record ServiceCacheVc SCr;

  SCr.Code = "LOGINTOTSEKKEE";
  SCr.Partner = "TSEKKEE";
  if(ReadFirstMain(SCr,2,true)) then begin
    if (nonblank(token)) then begin
      res = "https://" & SCr.ServiceHost & SCr.FuncName & "/" & partnerName & "/cloudsignin/" & token;
    end else begin
      res = "";
    end;
  end else begin
    res = "";
    SimpleDebugMsg("Costpocket","Login service cache is NOT found.");
  end;
  return;
end;