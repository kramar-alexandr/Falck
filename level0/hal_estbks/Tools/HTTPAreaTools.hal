external function Integer InString2(string,string);

function boolean ValidHttpCode(string codeToCheck)
begin
  boolean res;
  string 255 validCodes;
  
  validCodes = "100,101,200,201,202,203,204,205,206,300,301,302,303,304,305,306,307,400,401,402,403,404,405,406,407,408,409,410,411,412,413,414,415,416,417,500,501,502,503,504,505";
  res = false;
  if (InString2(validCodes,codeToCheck) != -1) then begin
    res = true;
  end;
  ValidHttpCode = res;
  return;
end;

global
function integer ParseFullHTTPHeader(area httpReply, var vector string paramValues)
begin
  integer res;
  string 255 currentLine,headParamValue, headParamName;
  area temp;
  boolean headerParsed;
  headerParsed = false;

  if (GetAreaLength(httpReply) <= 0) then begin
    res = -1;
    goto LParseFullHTTPHeader;
  end;

  while (headerParsed == false) begin
    currentLine = GetLineFromArea(httpReply,res);
    if (currentLine == chr(13) OR currentLine == "") then begin
      headerParsed = true;
    end;
    if (headerParsed == false) then begin
      headParamName = left(currentLine,InString2(currentLine," "));
      if (right(headParamName,1) == ":") then begin //remove colon from name
        headParamName = left(headParamName,len(headParamName)-1);
      end;
      headParamValue = right(currentLine,len(currentLine)-InString2(currentLine," ")-1);
      headParamValue = left(headParamValue,len(headParamValue)-1); //to exclude new line symbol
      if (headParamName == "HTTP/1.1" OR headParamName == "HTTP/1.0") then begin
        headParamName = "Status";
        if (!ValidHttpCode(left(headParamValue,3))) then begin
          res = -1;
          goto LParseFullHTTPHeader;
        end;
      end;
      paramValues[headParamName] = headParamValue;
      res = res + 1;
    end;
  end;
LParseFullHTTPHeader:;
  ParseFullHTTPHeader = res;
  return;
end;

global
procedure StripHTTPHeaderFromReply(var area httpReply)
begin
  longint bodyStartPos;
  area body;

  bodyStartPos =  FindStringInArea(chr(13) & chr(10) & chr(13) & chr(10),httpReply);
  bodyStartPos = bodyStartPos + 4; //length of two line ends
  GetAreaFromArea(httpReply,bodyStartPos,GetAreaLength(httpReply)-bodyStartPos,body);
  SetAreaZeroSize(httpReply);
  AddAreaToArea(body,httpReply);
end;
