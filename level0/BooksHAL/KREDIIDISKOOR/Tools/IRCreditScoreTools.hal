remote function boolean IRServiceActivated(string);
remote function time GetCredScoreUpdTimeRemote();
remote function time GetEMTAKUpdTimeRemote();

global
function boolean CredScoreCanExist(record CUVc CUr)
begin
  boolean res;

  res = true;
  if (CUr.CUType == 0 AND CUr.VEType == 0 AND CUr.LeadType == 0) then begin
    res = false;
  end;
  if (blank(CUr.RegNr1)) then begin
    res = false;
  end;
  CredScoreCanExist = res;
  return;
end;

global
function boolean ValidCreditScore(val creditScore)
begin
  boolean res;
  
  res = false;
  if (creditScore >= 0 AND creditScore <= 1 AND nonblank(creditScore)) then begin
    res = true;
  end;
  ValidCreditScore = res;
  return;
end;

global
function integer CreditScoreToColor(val creditScore)
begin
  integer res;

  res = kButtonColourWhite;
  if (!ValidCreditScore(creditScore)) then begin
    goto LCreditScoreToColor;
  end;
  if (creditScore <= 0.31 AND creditScore >= 0) then begin
    res = kButtonColourLime;
    goto LCreditScoreToColor;
  end;
  if (creditScore <= 0.41 AND creditScore > 0.31) then begin
    res = kButtonColourGreen;
    goto LCreditScoreToColor;
  end;
  if (creditScore <= 0.52 AND creditScore > 0.41)then begin
    res = kButtonColourDeepYellow;
    goto LCreditScoreToColor;
  end;
  if (creditScore <= 0.73 AND creditScore > 0.52)then begin
    res = kButtonColourOrange;
    goto LCreditScoreToColor;
  end;
  if (creditScore <= 1.00 AND creditScore > 0.73)then begin
    res = kButtonColourRed;
    goto LCreditScoreToColor;
  end;
LCreditScoreToColor:;
  CreditScoreToColor = res;
  return
end;

global
function string 5 GetCreditScoreString(val creditScore)
begin
  string 5 resString;

  if (!ValidCreditScore(creditScore)) then begin
    resString = "0.00";
  end else begin
    resString = ValToString(creditScore,M4Val,"",".",0);
  end;
  GetCreditScoreString = resString;
  return
end;

global
function time GetCredScoreUpdTime()
begin
  time res;
  res = GetCredScoreUpdTimeRemote;
  GetCredScoreUpdTime = res;
  return;
end;

global
function time GetEMTAKUpdTime()
begin
  time res;
  res = GetEMTAKUpdTimeRemote;
  GetEMTAKUpdTime = res;
  return;
end;
