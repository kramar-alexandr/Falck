// <halrule>server-only</halrule>

external function Boolean CheckInternetEnabler();
external updating procedure SaveERecordStatus(string,string,string,string,Integer,string,string,string,LongInt,Integer,string,string);
external function string 100 CreateCompID();
external function LongInt DateDiff(Date,Date);
external procedure GetItemVATCode(string,Integer,var string,Boolean);
external procedure GetINCostAcc(record INVc,var string);
external function Boolean ReadFirstItem(string,var record INVc,Boolean,Boolean);
external procedure ExtractObj(string,var Integer,var string);
//hal/exports
external procedure eInvoiceExportXMLHeader2(var area,var area,Integer,Boolean,string);
external function Boolean eInvoiceExportXML3(record IVVc,var area,var area,Integer,Integer,Boolean,Boolean,LongInt,Boolean,Integer,string);
//xml tools
external procedure EstEInvBuildString2(string,string,var string);
external procedure EstEInvBuildString(string,string,integer,var string);
external procedure EstEInvTagValue(string,string,integer,area); 
external procedure EstEInvStartTag2(string,var string,area);
external procedure EstEInvStartTag(string,area);
external procedure EstEInvEndTag(string,area);
external procedure GetTagContents(string ,var area);
external procedure LogAreaToFile(string,area);
external function val XmlGetVal(var xml,string,Integer,string,string);
external function date XmlGetDate(var xml,string,string);
//txt tools
external procedure CheckEstEInvoicingUsage2();
external procedure LookupForEstCustServices();
external procedure ExtractObjWithSeparator(string,string,Boolean,var Integer,var string);
external function string 50 TimeToString(time,string,boolean);
//Sales Invoice import
external updating function boolean EArveXML3In(xml,string,Integer,var LongInt);
//auto VIVc/ExpVc
external function Boolean GetNextCustNr(var string);
external function LongInt ValidateVEIV(record VEIVVc,var string,var Integer);
external updating function LongInt CreateVIFromVEIV2(LongInt,var record VIVc,Boolean);

//area return
global
function Boolean DoSendRequestToPartnerWithArea(area a,var area reply,var xml xdata,string service,string partner,string RequestType,string param,string authphrase,boolean checkflag)
begin
  record ServiceCacheVc SCr;
  Boolean res;
  Integer errcode,alen,i,pos;
  string 60 tstr,targetTag,rTag;
  string 150 tstr2,user,paswd;
  string 255 trimauthphrase;
  Area head;//,reply;
  
  trimauthphrase = Trim(authphrase);
  if(Len(RequestType)>0) then begin
    SCr.Code = service;
    SCr.Partner = partner;
    if (ReadFirstMain(SCr,2,true)==true) then begin
    end else begin
      if (Left(SCr.Partner,11) == "EARVEKESKUS") then begin
        SCr.Code = service;
        if (ReadFirstMain(SCr,1,true)==true) then begin end;
      end;
    end;
    EstEInvStartTag("?xml version=""1.0"" encoding=""UTF-8""?",head);  
    if (Left(SCr.Partner,11) == "EARVEKESKUS") then begin 
      EstEInvBuildString2("xmlns:soapenv","http://schemas.xmlsoap.org/soap/envelope/",tstr);
      EstEInvBuildString2("xmlns:erp","http://e-arvetekeskus.eu/erp",tstr);
      EstEInvStartTag2("soapenv:Envelope",tstr,head);
      EstEInvTagValue("soapenv:Header","",0,head);
      EstEInvStartTag("soapenv:Body",head);
      tstr = param;
      rTag = "erp:" & RequestType & "Request";
      EstEInvBuildString2("authPhrase",trimauthphrase,tstr);
      EstEInvStartTag2(rTag,tstr,head);
    end;
    if (Left(SCr.Partner,7) == "ENVOICE") then begin 
      EstEInvBuildString2("xmlns:SOAP-ENV","http://schemas.xmlsoap.org/soap/envelope/",tstr);
      EstEInvBuildString2("xmlns:erp","http://envoice.ee/api",tstr);
      EstEInvStartTag2("SOAP-ENV:Envelope",tstr,head);
      EstEInvStartTag("SOAP-ENV:Body",head);
      tstr = param;
      rTag = "erp:" & RequestType & "Request";
      EstEInvBuildString2("authPhrase",trimauthphrase,tstr);
      EstEInvStartTag2(rTag,tstr,head);
    end;
    if (Left(SCr.Partner,5) == "FITEK") then begin 
      EstEInvBuildString2("xmlns:soapenv","http://schemas.xmlsoap.org/soap/envelope/",tstr);
      EstEInvBuildString2("xmlns:iws","http://iws.itella.ee",tstr);
      EstEInvStartTag2("soapenv:Envelope",tstr,head);
      EstEInvStartTag("soapenv:Header",head);
      tstr = "";
      EstEInvBuildString2("xmlns:wsse","http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-secext-1.0.xsd",tstr);
      EstEInvBuildString2("soapenv:mustUnderstand","1",tstr);
      EstEInvStartTag2("wsse:Security",tstr,head);
      EstEInvStartTag("wsse:UsernameToken",head);
      ExtractObjWithSeparator(":",trimauthphrase,true,pos,user);
      paswd = Right(trimauthphrase,len(trimauthphrase)-len(user)-1);
      EstEInvTagValue("wsse:Username",user,100,head); 
      EstEInvTagValue("wsse:Password",paswd,100,head); 
      EstEInvEndTag("wsse:UsernameToken",head);
      EstEInvEndTag("wsse:Security",head);
      EstEInvEndTag("soapenv:Header",head);
      EstEInvStartTag("soapenv:Body",head);
      //tstr = param;
      rTag = "iws:" & RequestType;
      EstEInvStartTag2(rTag,tstr,head);
      if (RequestType == "getBuyInvoice") then begin
        EstEInvTagValue("iws:RegNo",param,100,head);     
      end;
      if (RequestType == "getAttachment") then begin
        EstEInvTagValue("iws:Id",param,100,head);     
        EstEInvTagValue("iws:type","0",100,head);     
      end;
    end;
    if(len(trimauthphrase)>0) then begin
      InsertAreaBeforeArea(head,a);  
      EstEInvEndTag(rTag,a);
      if (Left(SCr.Partner,7) == "ENVOICE") then begin
        EstEInvEndTag("SOAP-ENV:Body",a);
        EstEInvEndTag("SOAP-ENV:Envelope",a);
      end else begin 
        EstEInvEndTag("soapenv:Body",a);
        EstEInvEndTag("soapenv:Envelope",a);
      end;
//  Debug
      if (checkflag) then begin
        LogAreaToFile("debug/request/gc_initialize."&CountFilesInDir("debug/request/")&".request.xml",a); //TODO: remove
      end;
      res = SendWebRequest(SCr.ServiceHost,SCr.ServicePort,-1,true,"POST",SCr.FuncName,"text/xml; charset=utf-8","",false,a,reply,30);
      if (res) then begin
        xdata = ParseXmlArea(reply);
        if (Left(SCr.Partner,11) == "EARVEKESKUS") then begin 
          if (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring")) then begin
            tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode");
            tstr2 = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring");
            tstr2 = Left(tstr2,50);
            if (checkflag) then begin
              LogText(0,"Message from eArveKeskus: Errcode (" & StringToInt(LastInRange(tstr,5)) & ") " & tstr2);
            end;
            res = false;
          end;
        end;
        if (Left(SCr.Partner,5) == "FITEK") then begin         
          rTag = "ns:" & RequestType & "Response"; 
          if (XmlNodeExists(xdata,"soapenv:Envelope/soapenv:Body/" & rTag & "/ns:return/error")) then begin
            tstr = XmlGet(xdata,"soapenv:Envelope/soapenv:Body/" & rTag & "/ns:return/error/code");
            tstr2 = XmlGet(xdata,"soapenv:Envelope/soapenv:Body/" & rTag & "/ns:return/error/info");
            tstr2 = Left(tstr2,50);
            if (checkflag) then begin
              LogText(0,"Message from Fitek: Errcode (" & StringToInt(LastInRange(tstr,5)) & ") " & tstr2);
            end;
            res = false;
          end;
        end;
        if (Left(SCr.Partner,7) == "ENVOICE") then begin 
          if (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode/")) then begin
            tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode/");
            if (tstr!="0") then begin
              tstr2 = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring/");
              tstr2 = Left(tstr2,50);
              if (checkflag) then begin
                LogText(0,"Message from Envoice: Errcode (" & StringToInt(LastInRange(tstr,5)) & ") " & tstr2);
              end;
              res = false;
            end;
          end;
        end;
      end;
 // Debug
      if (checkflag) then begin
        LogAreaToFile("debug/reply/gc_initialize."&CountFilesInDir("debug/reply/")&".reply.xml",reply); //TODO: remove
      end;
    end else begin
      LogText(0,"DoSendRequestToPartner: Sending HALTED. Sender UsernameAtPartner==blank");
      res = false;
    end;
  end;
  res = true;
  DoSendRequestToPartnerWithArea = res;
  return;
end;

//ts - 2018/04, saving e-service statistic
global
updating procedure StoreCUServiceUsage(date ServiceDate,string RegNr1,Integer ContractType,string Partner,Integer ServiceReq,Integer Volume)
begin
  record CUServiceUsageVc CUSUr,oldCUSUr;
  
  CUSUr.ServiceDate = ServiceDate;
  CUSUr.RegNr1 = RegNr1;
  CUSUr.ContractType = ContractType;
  CUSUr.Partner = Partner;
  if (ReadFirstMain(CUSUr,4,true))  then begin
    RecordCopy(oldCUSUr,CUSUr);
    CUSUr.ServiceReq = CUSUr.ServiceReq + ServiceReq;
    CUSUr.Volume = CUSUr.Volume + Volume;
    RecordUpdate(oldCUSUr,CUSUr,true);
  end else begin
    RecordNew(CUSUr);
    CUSUr.ServiceDate = ServiceDate;
    CUSUr.RegNr1 = RegNr1;
    CUSUr.ContractType = ContractType;
    CUSUr.Partner = Partner;
    CUSUr.ServiceReq = ServiceReq;
    CUSUr.Volume = Volume;
    RecordStore(CUSUr,false);
  end;
  return;
end;

function boolean DoEstEInvoiceExportXML(record IVVc IVr,var area stack,var area attach,record EInvoiceBlock EIb,LongInt version,string partner)
begin
  Boolean res,attachpdf,einvcuf,shortnotef;
  record CUVc CUr;
  Integer RcvInvoicePref,RcvInvoicePref2;
  
  RcvInvoicePref = 0; RcvInvoicePref2 = 0;
  attachpdf = EIb.AttachPDFtoElectronicInvoice;
  CUr.Code = IVr.CustCode;
  einvcuf = ReadFirstMain(CUr,1,true);
  if (einvcuf) then begin
    if (CUr.eInvRcvPref != 0) then begin
      attachpdf = CUr.eInvAttachPDF;
      RcvInvoicePref = CUr.eInvRcvPref;
      RcvInvoicePref2 = CUr.eInvAltRcvPref;
      shortnotef = CUr.eInvShortNote;
    end;
  end;
  if (RcvInvoicePref==0) then begin
    RcvInvoicePref = EIb.RcvInvoicePref;
  end;
  if (RcvInvoicePref==3) then begin//tmp, for sending to partner without channels
    RcvInvoicePref = 0;
  end;
  res = eInvoiceExportXML3(IVr,stack,attach,RcvInvoicePref,RcvInvoicePref2,attachpdf,shortnotef,version,EIb.NoLineswoItemonEInv==1,EIb.ExportIVSendPlainINasStockIN,partner);
  DoEstEInvoiceExportXML = res;
  return;
end;
  
function Boolean PrepareEstEInvoiceForSending(string partner,record EInvoiceQueVc EInvoiceQuer,var area stack,var area attach,record EInvoiceBlock EIb)
begin
  Boolean res;
  record IVVc IVr;
  
  switch (EInvoiceQuer.FileName) begin
    case "estIVVc": 
      IVr.SerNr = EInvoiceQuer.RecSerNr;
      if (ReadFirstMain(IVr,1,true)) then begin
        res = DoEstEInvoiceExportXML(IVr,stack,attach,EIb,EInvoiceQuer.RecVersionNr,partner);
      end;
  end;
  PrepareEstEInvoiceForSending = res;
  return;
end;

function boolean SendInvoiceToEArvekeskus(var area invoices,var area attachments,string service,string partner,var area reply,var xml xdata,record EInvoiceBlock EIb,boolean checkflag)
begin
  string 5 status;
  string 100 param;
  Boolean res;

  status = -1;
  res = false;
  eInvoiceExportXMLHeader2(invoices,attachments,EIb.TestFlag,false,"");
  DoSendRequestToPartnerWithArea(invoices,reply,xdata,service,partner,"EInvoice",param,EIb.PrivateKey,checkflag);
  status = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/erp:EInvoiceResponse/ErrorCode");
  if(status=="0") then begin
    res = true;
  end else begin
//    LogText(0,"SendInvoiceToEArvekeskus: no answer");
  end;
  SetAreaZeroSize(invoices);
  SendInvoiceToEArvekeskus = res;
  return;
end;

function boolean SendInvoiceToFitek(var area invoices,var area attachments,string service,string partner,var area reply,var xml xdata,record EInvoiceBlock EIb,boolean checkflag)
begin
  string 5 status;
  string 100 param;
  Boolean res;

  status = -1;
  res = false;
  eInvoiceExportXMLHeader2(invoices,attachments,EIb.TestFlag,false,"FITEK");
  DoSendRequestToPartnerWithArea(invoices,reply,xdata,service,partner,"sendSaleInvoice",param,EIb.PrivateKey,checkflag);
  status = XmlGet(xdata,"soapenv:Envelope/soapenv:Body/ns:sendSaleInvoiceResponse/ns:return/status");
  if(status=="0") then begin
    res = true;
  end else begin
//    LogText(0,"SendInvoiceToFitek: no answer");
  end;
  SetAreaZeroSize(invoices);
  SendInvoiceToFitek = res;
  return;
end;

function boolean SendInvoiceToEnvoice(var area invoices,var area attachments,string service,string Partner,var area reply,var xml xdata,record EInvoiceBlock EIb,boolean checkflag)
begin
  string 5 status;
  string 100 param;
  Boolean res;

  status = -1;
  res = false;
  eInvoiceExportXMLHeader2(invoices,attachments,EIb.TestFlag,false,"ENVOICE");
  DoSendRequestToPartnerWithArea(invoices,reply,xdata,service,partner,"EInvoice",param,EIb.PrivateKey,checkflag);
  status = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/ns1:EInvoiceResponse/ns1:ErrorCode");
  if(status=="0") then begin
    res = true;
  end else begin
//    LogText(0,"SendInvoiceToFitek: no answer");
  end;
  SetAreaZeroSize(invoices);
  SendInvoiceToEnvoice = res;
  return;
end;
function boolean SendInvoiceToPartner(string partner,var area invoices,var area attachments,string service,var area reply,var xml xdata,record EInvoiceBlock EIb,boolean checkflag)
begin
  Boolean res;

  switch (partner) begin
    case "EARVEKESKUS": res = SendInvoiceToEArvekeskus(invoices,attachments,service,partner,reply,xdata,EIb,checkflag);
    case "FITEK":       res = SendInvoiceToFitek(invoices,attachments,service,partner,reply,xdata,EIb,checkflag);
    case "ENVOICE":       res = SendInvoiceToEnvoice(invoices,attachments,service,partner,reply,xdata,EIb,checkflag);
  end;
  SendInvoiceToPartner = res;
  return;
end;

procedure FindFirstDateOfERecords(string FileName,var date SendDate,var time SendTime)
begin
  record ERecordStatusVc ERecordStatusr;
  
  if (ReadFirstMain(ERecordStatusr,1,true)) then begin
    SendDate = ERecordStatusr.CreationDate;
    SendTime = ERecordStatusr.CreationTime;
  end;
  return;
end;

procedure SendInvoiceStatusRequest(record EInvoiceBlock EIb,string service,string partner,boolean checkflag,var area reply,string status)
begin
  area a;
  string 100 param;
  date SendDate;
  time SendTime;
  xml xdata;

  SendDate = EIb.LastOutEstEInvStatusDate;
  SendTime = EIb.LastOutEstEInvStatusTime;
  if (blankdate(SendDate)) then begin
    FindFirstDateOfERecords("estIVVc",SendDate,SendTime);
  end;
  SetAreaZeroSize(a);
  EstEInvTagValue("erp:state",status,10,a);
  EstEInvBuildString2("since",datetostring(SendDate,"DD-MM-YYYY") & " " & SendTime,param);  
  DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"SaleInvoiceExport",param,EIb.PrivateKey,checkflag);
  return;
end;

procedure DoReceiveEstEInvoiceStatus(record EInvoiceBlock EIb,string service,string partner,boolean checkflag,var area reply,string status)
begin
  SendInvoiceStatusRequest(EIb,service,partner,checkflag,reply,status);
  return;
end;

global
updating procedure ChangeERecordStatus(string FileName,LongInt RecSerNr,Integer status,string comment,string CustomerCode)
BEGIN
  record ERecordStatusVc ERecordStatusr,oldERecordStatusr;
  record CYBlock CYb;
  string 100 CompID;
  
  ERecordStatusr.FileName = FileName;
  ERecordStatusr.CustID = CustomerCode;
  ERecordStatusr.CustSerNr = RecSerNr;
  ERecordStatusr.CompNr = CurrentCompany;
  if (ReadLastKey("FileName",ERecordStatusr,4,true)) then begin
    RecordCopy(oldERecordStatusr,ERecordStatusr);
    ERecordStatusr.Status = status;
    ERecordStatusr.Comment = comment;
    if (RecordUpdate(oldERecordStatusr,ERecordStatusr,false)) then begin end;    
  end else begin
    BlockLoad(CYb);
    CompID = CreateCompID;
    SaveERecordStatus(FileName,RecSerNr,CustomerCode,USetStr(1150),status,CompID,comment,"EARVEKESKUS",0,1,CYb.VATNr,CYb.OrgNr);
  end;
  RETURN;
END;

global
updating procedure ProcessReceivedReply(area reply,string CustomerCode,Integer status)
begin
  string 255 xkey;
  Integer i;
  LongInt RecSerNr;
  xml xdata;

  xdata = ParseXmlArea(reply);
  
  xkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/SaleInvoiceExportResponse/";
  for (i=0; XmlNodeExists(xdata,xkey & "E_Invoice/Invoice[" & i & "]"); i=i+1) begin
    RecSerNr = XmlGet(xdata,xkey & "E_Invoice/Invoice[" & i & "]/InvoiceInformation/InvoiceNumber");
    ChangeERecordStatus("estIVVc",RecSerNr,status,UsetStr(33534),CustomerCode);//33034 > 33534
  end;
  return;
end;

procedure SendSupplierInvoiceRequest(record EInvoiceBlock EIb,record CYBlock CYb,string service,string partner,var area reply,string status,var date SendDate,var time SendTime)//ts - 2018/04, added parameter
begin
  record VEIVVc VEIVr;
  string 100 param,tstr;
  area a;
  xml xdata;

  SendDate = EIb.LastInEstEInvoicesDate;
  SendTime = EIb.LastInEstEInvoicesTime;
  if (blankdate(SendDate)) then begin
    if (ReadFirstMain(VEIVr,1,true)) then begin
      SendDate = VEIVr.ServiceDelDate;
      SendTime = VEIVr.TransTime;
    end;
  end;
  if (blankdate(SendDate)) then begin
    SendDate = AddDay(CurrentDate,-GetDay(CurrentDate)+1);
    SendTime = CurrentTime;
    if (partner == "ENVOICE2") then begin
      SendTime = AddHours(SendTime,-3);
      if (SendTime.hour >= 21) and (SendTime.hour <= 23) then begin
        SendDate = AddDay(SendDate,-1);
      end;
    end;
  end;
  SetAreaZeroSize(a);
  if (partner == "FITEK2") then begin
    param = CYb.OrgNr;
    DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"getBuyInvoice",param,EIb.PrivateKey,(EIb.DebugFlag==1));
  end;
  if (partner == "EARVEKESKUS2") then begin
    if (EIb.DblAcceptEPay == 0) then begin
      EstEInvTagValue("erp:state","RECEIVED",10,a);
    end else begin
      EstEInvTagValue("erp:state","VERIFIED",10,a);
    end;
    EstEInvBuildString2("since",datetostring(SendDate,"DD-MM-YYYY") & " " & SendTime,param);  
    DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"BuyInvoice",param,EIb.PrivateKey,(EIb.DebugFlag==1));
  end;
  if (partner == "ENVOICE2") then begin
    if (EIb.DblAcceptEPay == 0) then begin
      EstEInvTagValue("erp:state","RECEIVED",10,a);
    end else begin
      EstEInvTagValue("erp:state","VERIFIED",10,a);
    end;
    tstr = datetostring(SendDate,"YYYY-MM-DD") & "T" & TimeToString(SendTime,"HH",false) & ":" & TimeToString(SendTime,"MM",false) & ":" & TimeToString(SendTime,"SS",false) & "+00:00";
    EstEInvBuildString2("since",tstr,param);
    DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"BuyInvoiceExport",param,EIb.PrivateKey,(EIb.DebugFlag==1));
  end;
  return;
end;

procedure SendSupplierInvoicePdfRequest(record EInvoiceBlock EIb,string service,string partner,boolean checkflag,var area reply,var xml xdata,Integer aCnt,array string aInvoiceId,Integer startIndex)
begin
  record VEIVVc VEIVr;
  area a;
  Integer i;
  string 100 param;

  SetAreaZeroSize(a);
  switch (partner) begin
    case "FITEK2":
      param = aInvoiceId[0];
      DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"getAttachment",param,EIb.PrivateKey,checkflag);
    otherwise
      for (i=0; i<aCnt; i=i+1) begin
        EstEInvTagValue("erp:invoiceId",aInvoiceId[i],30,a);
      end;
      if (EIb.IncAllAttach == 1) then begin
        EstEInvBuildString2("onlyInvoice","NO",param);
      end else begin
        EstEInvBuildString2("onlyInvoice","YES",param);
      end;
      EstEInvBuildString2("startIndex",startIndex,param);  
      DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"InvoiceAttachment",param,EIb.PrivateKey,checkflag);
  end;
  return;
end;

procedure SendSupplierEstEInvoiceStatus(record EInvoiceBlock EIb,string service,boolean checkflag,area a,var xml xdata,var Boolean res)
begin
  string 100 param;
  area reply;

  res = DoSendRequestToPartnerWithArea(a,reply,xdata,service,EIb.SuppInvoicePartner,"BuyInvoiceRegistered",param,EIb.PrivateKey,checkflag);
  return;
end;

procedure SendSupplierRejEstEInvoiceStatus(record EInvoiceBlock EIb,string service,boolean checkflag,area a,var xml xdata,string regnr,string namestr)
begin
  string 100 param;
  area reply;

  EstEInvBuildString2("regNumber",regnr,param);  
  EstEInvBuildString2("name",namestr,param);  
  DoSendRequestToPartnerWithArea(a,reply,xdata,service,EIb.SuppInvoicePartner,"RejectConfirmation",param,EIb.PrivateKey,checkflag);
  return;
end;

procedure AddStatusToeArveKeskusReply(record ERecordStatusVc ERecordStatusr,var Area a)
begin
  string 100 param;
  
  EstEInvBuildString2("invoiceId",ERecordStatusr.CustRecRowNr,param);
  EstEInvStartTag2("erp:RegisteredInvoice",param,a);
  EstEInvTagValue("erp:ErpDocumentNumber",ERecordStatusr.CustSerNr,250,a);
  EstEInvEndTag("erp:RegisteredInvoice",a);

  return;
end;

procedure AddRejStatusToeArveKeskusReply(record ERecordStatusVc ERecordStatusr,string comment,var Area a)
begin
  string 100 param;

  EstEInvBuildString2("rejectType","REJECT",param);
  EstEInvBuildString2("invoiceId",ERecordStatusr.CustRecRowNr,param);
  EstEInvStartTag2("erp:RejectContent",param,a);
  AddTextToArea(comment,a);
  EstEInvEndTag("erp:RejectContent",a);

  return;
end;

updating procedure ReleaseOnHoldRecords(string filename)
begin
  record EInvoiceQueVc EInvoiceQuer,oldEInvoiceQuer;
  boolean TrHs;

  TrHs = true;
  EInvoiceQuer.Status = 2;
  EInvoiceQuer.FileName = filename;
  while (LoopKey("Status",EInvoiceQuer,2,TrHs)) begin
    if (EInvoiceQuer.Status <> 2) then begin
      TrHs = false;
    end;
    if (EInvoiceQuer.FileName <> filename) then begin
      TrHs = false;
    end;
    if (TrHs) then begin
      RecordCopy(oldEInvoiceQuer,EInvoiceQuer);      
      EInvoiceQuer.Status = 0;//0 - to be sent, 2 - on hold, 45 - failed
      EInvoiceQuer.Comment = "RELEASED";   
      RecordUpdate(oldEInvoiceQuer,EInvoiceQuer,false);
//      StepBack(EInvoiceQuer);
    end;
  end;
  return;
end;

//cust - begin: ts 2018/04
global
updating procedure SendInvoiceToPartnerOK(string filename,record CYBlock CYb,array string addedtostack,array string addedtostackq,Integer cnt,string CustomerCode)
begin
  record EInvoiceQueVc EInvoiceQuer,oldEInvoiceQuer;
  Integer i;

  for (i=0; i<cnt; i=i+1) begin
    EInvoiceQuer.SerNr = addedtostackq[i];
    if (ReadFirstMain(EInvoiceQuer,1,true)) begin
      SaveERecordStatus(filename,addedtostack[i],CustomerCode,USetStr(1150),2,EInvoiceQuer.CompID,USetStr(33532),"EARVEKESKUS",EInvoiceQuer.RecVersionNr,1,CYb.VATNr,CYb.OrgNr);//33032 > 33532
      RecordCopy(oldEInvoiceQuer,EInvoiceQuer);      
      EInvoiceQuer.Status = 18;
      EInvoiceQuer.Comment = "SENT";
      if (RecordUpdate(oldEInvoiceQuer,EInvoiceQuer,false)) then begin
      end;
    end;
  end;
  return;
end;

global
updating procedure SendInvoiceToPartnerFAILED(string filename,record CYBlock CYb,area reply,array string addedtostack,array string addedtostackq,Integer cnt,string CustomerCode)
begin
  record EInvoiceQueVc EInvoiceQuer,oldEInvoiceQuer;
  record NotepadVc Noter;
  string 255 tstr,tstr2;
  Integer i,f;
  xml xdata;
  
  xdata = ParseXmlArea(reply);

  i = 0;
  EInvoiceQuer.SerNr = addedtostackq[i];
  EInvoiceQuer.FileName = filename;
  if (ReadFirstMain(EInvoiceQuer,2,true)) begin
    RecordCopy(oldEInvoiceQuer,EInvoiceQuer);      
    if (EInvoiceQuer.Status == 2) then begin
      EInvoiceQuer.Status = 45;//0 - to be sent, 2 - on hold, 45 - failed
      EInvoiceQuer.Comment = "FAILED";
//create note !
      RecordNew(Noter);
      Noter.SerNr = NextSerNr("NotepadVc",CurrentDate,-1,false,"");
      f = 0;
      while (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring[" & f & "]")) begin
        tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode[" & f & "]");
        tstr2 = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring[" & f & "]");
        f = f + 1;
        AddToText(tstr,Noter);
        AddToText(Chr(13) & Chr(10),Noter);
        AddToText(tstr2,Noter);
        AddToText(Chr(13) & Chr(10),Noter);
      end;
//fitek
      if (XmlNodeExists(xdata,"soapenv:Envelope/soapenv:Body/ns:sendSaleInvoiceResponse/ns:return/error")) begin
        tstr = XmlGet(xdata,"soapenv:Envelope/soapenv:Body/ns:sendSaleInvoiceResponse/ns:return/error/code");
        tstr2 = XmlGet(xdata,"soapenv:Envelope/soapenv:Body/ns:sendSaleInvoiceResponse/ns:return/error/info");
        AddToText(tstr,Noter);
        AddToText(Chr(13) & Chr(10),Noter);
        AddToText(tstr2,Noter);
        AddToText(Chr(13) & Chr(10),Noter);
      end;
      if (RecordStore(Noter,false)) then begin
        CreateRecordLink(EInvoiceQuer,CurrentCompany,Noter,CurrentCompany);
      end;
//end create note
      if (RecordUpdate(oldEInvoiceQuer,EInvoiceQuer,false)) then begin
      end;
      ReleaseOnHoldRecords(filename);
    end else begin
      for (i=0; i<cnt; i=i+1) begin
        EInvoiceQuer.SerNr = addedtostackq[i];
        EInvoiceQuer.FileName = filename;
        if (ReadFirstMain(EInvoiceQuer,2,true)) begin
          RecordCopy(oldEInvoiceQuer,EInvoiceQuer);      
          EInvoiceQuer.Status = 2;//0 - to be sent, 2 - on hold, 45 - failed
          EInvoiceQuer.Comment = "ON HOLD";
          if (RecordUpdate(oldEInvoiceQuer,EInvoiceQuer,false)) then begin
          end;
        end;
      end;
    end;
  end;
  return;
end;
//cust - end

procedure SendEstEInvoiceFromCurCompany(record EInvoiceBlock EIb,record CYBlock CYb,string service,string partner,string CustomerCode,string filename,var Integer volume)//ts - 2018/04, added parameter for e-service statistic
begin
  record EInvoiceQueVc EInvoiceQuer;
  Area invoices,attachments;
  LongInt exitlimit,sizelimit,packagesize,sentsize,accumsize;
  array string 40 addedtostack,addedtostackq;
  Boolean TrHs;
  Integer maxcnt,cnt;
  xml xdata;
  area reply;
  
  sizelimit = 10000;  
  exitlimit = 30000;

  cnt = 0; volume = 0;
  EInvoiceQuer.Status = 2;
  EInvoiceQuer.FileName = filename;
  if (ReadFirstKey("Status",EInvoiceQuer,2,true)) then begin
    if (PrepareEstEInvoiceForSending(partner,EInvoiceQuer,invoices,attachments,EIb)) then begin
      packagesize = GetAreaLength(invoices)+GetAreaLength(attachments);
      addedtostack[cnt] = EInvoiceQuer.RecSerNr;
      addedtostackq[cnt] = EInvoiceQuer.SerNr;
      cnt = cnt + 1;
    end;
  end;
  if (cnt <= 0) then begin
    maxcnt = EIb.CntOutEstEInvoices;
    if (maxcnt <= 0) then begin
      maxcnt = 10;
    end;
    TrHs = true;
    EInvoiceQuer.Status = 0;
    EInvoiceQuer.FileName = filename;
    while (LoopKey("Status",EInvoiceQuer,2,TrHs)) begin
      if (EInvoiceQuer.Status!=0) then begin
        TrHs = false;
      end;
      if (EInvoiceQuer.FileName<>filename) then begin
        TrHs = false;
      end;
      if (cnt>=maxcnt) then begin TrHs = false; end;
      if (TrHs) then begin
        if (PrepareEstEInvoiceForSending(partner,EInvoiceQuer,invoices,attachments,EIb)) then begin
          packagesize = GetAreaLength(invoices)+GetAreaLength(attachments);
          addedtostack[cnt] = EInvoiceQuer.RecSerNr;
          addedtostackq[cnt] = EInvoiceQuer.SerNr;
          cnt = cnt + 1;
          if (EInvoiceQuer.Comment!="RELEASED") then begin
            volume = volume + 1;
          end;
        end;
      end;
    end;
  end;
  if (cnt > 0) then begin  
    if (SendInvoiceToPartner(partner,invoices,attachments,service,reply,xdata,EIb,(EIb.DebugFlag == 1))) then begin
      qupdating.SendInvoiceToPartnerOK(filename,CYb,addedtostack,addedtostackq,cnt,CustomerCode);
    end else begin
      qupdating.SendInvoiceToPartnerFAILED(filename,CYb,reply,addedtostack,addedtostackq,cnt,CustomerCode);
    end;
  end;
  return;
end;

//click server
global
procedure SendEstEServiceUsageIdleTask(string arg)
begin
  CheckEstEInvoicingUsage2;
  return;
end;

global
procedure SendEstEInvoiceIdleTaskRemoveIdleTask2()
begin
  if (RemoveTask("SendEstEInvoice")) then begin
  end;
  if (RemoveTask("SentEstEInvoiceStatus")) then begin
  end;
  return;
end;

global
procedure SendEstEInvoiceIdleTaskRemoveIdleTask(boolean noEServ)
begin
  SendEstEInvoiceIdleTaskRemoveIdleTask2;
  return;
end;

global
procedure GetEstEInvoiceIdleTaskRemoveIdleTask()
begin
  if (RemoveTask("GetEstEInvoice")) then begin
  end;
  return;
end;

global
procedure ReceiveEstEInvoiceIdleTaskRemoveIdleTask2()
begin
  if (RemoveTask("ReceiveEstEInvoice")) then begin
  end;
  if (RemoveTask("ReceiveEstEInvoicePdf")) then begin
  end;
  if (RemoveTask("SendEstSupEInvoiceStatus")) then begin
  end;
  return;
end;

global
procedure ReceiveEstEInvoiceIdleTaskRemoveIdleTask(Boolean noEServ)
begin
  ReceiveEstEInvoiceIdleTaskRemoveIdleTask2;
  return;
end;

global
updating procedure UpdateSendEstEInvoice(record EInvoiceBlock EIb,date sdate,time stime)
begin
  EIb.LastOutEstEInvoicesDate = sdate;
  EIb.LastOutEstEInvoicesTime = stime;
  BlockStore(EIb);
  return;
end;

global
updating procedure UpdateSendEstSupEInvoice(record EInvoiceBlock EIb,date sdate,time stime)
begin
  EIb.LastInEstEInvStatusDate = sdate;
  EIb.LastInEstEInvStatusTime = stime;
  BlockStore(EIb);
  return;
end;

global
updating procedure UpdateSentEstEInvoiceStatus(record EInvoiceBlock EIb,date sdate,time stime)
begin
  EIb.LastOutEstEInvStatusDate = sdate;
  EIb.LastOutEstEInvStatusTime = stime;
  BlockStore(EIb);
  return;
end;

global
updating procedure UpdateReceiveEstEInvoice(record EInvoiceBlock EIb,date sdate,time stime,date sdate2,time stime2)
begin
  EIb.LastInEstEInvoicesDate = sdate;
  EIb.LastInEstEInvoicesTime = stime;
  EIb.LastInEstDate = sdate2;
  EIb.LastInEstTime = stime2;
  BlockStore(EIb);
  return;
end;

global
updating procedure UpdateGetEstEInvoice(record EInvoiceBlock EIb,date sdate,time stime,date sdate2,time stime2)
begin
  EIb.LastInIVEstEInvoicesDate = sdate;
  EIb.LastInIVEstEInvoicesTime = stime;
  EIb.LastInIVEstDate = sdate2;
  EIb.LastInIVEstTime = stime2;
  BlockStore(EIb);
  return;
end;

global
updating procedure UpdateReceiveEstEInvoicePdf(record EInvoiceBlock EIb,LongInt LastChecked)
begin
  EIb.LastInEstEInvStatusSerial = LastChecked;
  BlockStore(EIb);
  return;
end;

global
procedure SendEstEInvoiceIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf,stopidletaskf;
  Integer i,cnt,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  record CYBlock CYb;
  record InternetEnablerBlock IEb;

  SCr.Code = "SENDESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    stopidletaskf = true;
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              stopidletaskf = false;
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine1;
            end;
          end;
          BlockLoad(EIb);
          if (EIb.StopOutEstEInvoices == 1) then begin
            if (EIb.OutEstEInvoices==1) then begin
              stopidletaskf = false;
            end;
            goto LNextCompanyLine1;
          end;
          if (EIb.DefaultEBehave==0) then begin 
            goto LNextCompanyLine1;
          end;
          testf = true;
          if (EIb.OutEstEInvoices==0) then begin 
            testf = false;
          end;
          if (testf) then begin 
            BlockLoad(IEb);
            BlockLoad(CYb);
            SendEstEInvoiceFromCurCompany(EIb,CYb,SCr.Code,EIb.EInvoicePartner,IEb.CustomerCode,"estIVVc",cnt);
            qupdating.UpdateSendEstEInvoice(EIb,CurrentDate,CurrentTime);
            if (cnt > 0) then begin
              queued.StoreCUServiceUsage(CurrentDate,CYb.OrgNr,0,EIb.EInvoicePartner,1,cnt);//nr of invoices sent, //cust, ts - 2018/04
            end;
            stopidletaskf = false;
          end;
        end;
      end;
LNextCompanyLine1:;
    end;
    ResetCompany(oldcomp);
    if (stopidletaskf) then begin
      SendEstEInvoiceIdleTaskRemoveIdleTask2;
    end;
  end;
  if (found==false) then begin
    LookupForEstCustServices;
  end;
  return;
end;

procedure ReceiveEstEInvoiceStatus(record EInvoiceBlock EIb,string service,boolean checkflag,string CustomerCode,Integer status,string type)
begin
  area reply;
  
  DoReceiveEstEInvoiceStatus(EIb,service,EIb.EInvoicePartner,checkflag,reply,type);
  qupdating.ProcessReceivedReply(reply,CustomerCode,status);
  return;
end;

global
procedure SentEstEInvoiceStatusIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf;
  Integer i,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  record InternetEnablerBlock IEb;
  Time t;

  SCr.Code = "SENDESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine2;
            end;
          end;
          if (EIb.StopOutEstEInvStatus == 1) then begin
            goto LNextCompanyLine2;
          end;
          if (EIb.DefaultEBehave==0) then begin 
            goto LNextCompanyLine2;
          end;
          testf = true;
          if (EIb.OutEstEInvoices==0) then begin 
            testf = false;
          end;
          if (EIb.EInvoicePartner!="EARVEKESKUS") then begin
            testf = false;
          end;
          if (testf) then begin
            BlockLoad(IEb);
            ReceiveEstEInvoiceStatus(EIb,SCr.Code,(EIb.DebugFlag),IEb.CustomerCode,4,"SENT");//imported
            t = AddSeconds(CurrentTime,1);
            qupdating.UpdateSentEstEInvoiceStatus(EIb,CurrentDate,t);
          end;
        end;
      end;
LNextCompanyLine2:;
    end;
    ResetCompany(oldcomp);
  end;
LSentEstEInvoiceStatusIdleTask:;
  return;
end;

procedure PutVATCodeToVEIVRow(row VEIVVc VEIVrw)
BEGIN
  record VATCodeBlock VCb;
  row VATCodeBlock VCrw;
  Integer i,rwcnt,j;
  string 20 vatcode;
  record APAccBlock APb;
  Boolean foundf;
  
  BlockLoad(VCb);
  vatcode = "";
  rwcnt = MatRowCnt(VCb);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(VCb,i,VCrw);
    if (VCrw.ExVatpr==VEIVrw.VEVATPrc) then begin
      vatcode = VCrw.VATCode;
      goto LPutVATCodeToVEIVRow;
    end;
  end;
  if (blank(vatcode)) then begin
    BlockLoad(APb);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(VCb,i,VCrw);
      if (VCrw.VATCode==APb.VATCodeDom) then begin
        if (VCrw.ExVatpr==VEIVrw.VEVATPrc) then begin
          vatcode = VCrw.VATCode;
          goto LPutVATCodeToVEIVRow;
        end;
      end;
    end;    
  end;
LPutVATCodeToVEIVRow:;  
  VEIVrw.VATCode = vatcode;
  RETURN;
END;

procedure PutOurDataToVEIV(record VEIVVc VEIVr)
BEGIN
  record VIVc VIr;
  record PIVc PIr;
  record CUVc VEr;
  record INVc INr;
  row VEIVVc VEIVrw;
  Integer i,rwcnt;
  string 255 vatcode,costacc;
  boolean foundf;

  VEr.Code = VEIVr.VECode;
  foundf = ReadFirstMain(VEr,1,true);
  if (foundf == false) then begin
    if(nonblank(VEIVr.VERegNr1)) then begin //cust fix KB 05.12.2017, otherwise wrong customer is pasted
      VEr.RegNr1 = VEIVr.VERegNr1;
      foundf = ReadFirstKey("VEActRegNr1",VEr,1,true);
      VEIVr.VECode = VEr.Code;
    end; //cust fix KB 05.12.2017
  end;
  if (VEIVr.VESerNr>0) then begin
    VIr.InvoiceNr = VEIVr.VESerNr;
  end else begin
    VIr.InvoiceNr = VEIVr.InvoiceNr;
  end;
  VIr.VECode = VEIVr.VECode;
  if (ReadFirstKey("InvoiceNr",VIr,2,true)) then begin
    VEIVr.VISerNr = VIr.SerNr;
    goto LPutOurDataToVEIV;
  end;
  rwcnt = MatRowCnt(VEIVr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(VEIVr,i,VEIVrw);
    if (VEIVrw.stp==1) then begin
      if (nonblank(VEIVrw.VEArtCode)) then begin
        PIr.VECode = VEIVr.VECode;
        PIr.VEItemCode = VEIVrw.VEArtCode;
        if (ReadFirstKey("VEItemCode",PIr,2,true)) then begin
          VEIVrw.ArtCode = PIr.ItemCode;
          VEIVrw.Quant = VEIVrw.VEQuant;
          if (PIr.PIFactor!=0) then begin
            VEIVrw.VEQuant = VEIVrw.VEQuant*PIr.PIFactor;
          end;
          vatcode = "";
          if (blank(vatcode)) then begin
            GetItemVATCode(VEIVrw.ArtCode,VEIVr.ExportFlag,vatcode,false);
          end;
          if (blank(VEIVrw.VATCode)) then begin
            VEIVrw.VATCode = vatcode;
          end;
          if (blank(VEIVrw.CostAcc)) then begin
            if (ReadFirstItem(VEIVrw.ArtCode,INr,true,false)) then begin
              GetINCostAcc(INr,costacc);
              VEIVrw.CostAcc = costacc;
            end;
          end;
        end;
      end;
    end;
    if (blank(VEIVrw.VATCode)) then begin
      PutVATCodeToVEIVRow(VEIVrw);
    end;
    MatRowPut(VEIVr,i,VEIVrw);
  end;
LPutOurDataToVEIV:;  
  RETURN;
END;

procedure BreakIntoBase64Lines(Area img,var area tmpimg)
begin
  longint i,l,x;
  integer colwidth;
  string 255 line;
  
  SetAreaZeroSize(tmpimg);
  i=0;
  l = GetAreaLength(img);
  while (i<l) begin
    x = l-i;
    if (x>80) then begin
      colwidth = 80;
    end else begin
      colwidth = x;
    end;
    line = GetStringFromArea(img,i,colwidth);
    AddTextToArea(line & chr(10),tmpimg);
    i = i+colwidth;
  end;
  return;
end;

global
updating procedure LinkFileToVEIVRecord(record VEIVVc VEIVr,string fname)
begin
  if (RecordLinkFile(fname,0,VEIVr,CurrentCompany)) then begin end;
  Delete_File(fname);
  return;
end;

global
updating procedure LinkFileToCUIVRecord(record IVVc IVr,string fname)
begin
  if (RecordLinkFile(fname,0,IVr,CurrentCompany)) then begin end;
  Delete_File(fname);
  return;
end;

//cust - begin: ts 2018/05
updating procedure DoCreateVIfromVEIV(record VEIVVc VEIVr)
begin
  record VEIVVc oldVEIVr;
  record VIVc VIr;
  LongInt r;
  string 255 fieldname,errstr;
  Integer rownr;
  
  if (VEIVr.NotPaidFlag==0) then begin
    r = ValidateVEIV(VEIVr,fieldname,rownr);
    if (r==0) then begin
      r = CreateVIFromVEIV2(VEIVr.SerNr,VIr,false);
      if (r==0) begin
        RecordCopy(oldVEIVr,VEIVr);
        VEIVr.VISerNr = VIr.SerNr;
        VEIVr.OKStatus = 1;
        if (RecordUpdate(oldVEIVr,VEIVr,true)!=0) then begin
        end;
      end else begin
          switch (r) begin
            case 1500025: errstr = "Ei ole mŠŠratud numbriseerias";
            case 1500026: errstr = "Ostuarve koostamine eba›nnestus";
            case 1500027: errstr = "Ostuarve koostamine eba›nnestus. Puudub summa";
            case 1260: errstr = "Sellist valuutat ei ole";
            case 1434: errstr = "Tarnijal puudub kulukonto";
            case 20808: errstr = "Ostuarve juba koostatud";
          end;
      end;
    end else begin
      errstr = "Ostuarve koostamiseks sisesta KM-kood";
    end;
    if (r > 0) then begin
      RecordCopy(oldVEIVr,VEIVr);
      VEIVr.Footer2 = errstr;
      if (RecordUpdate(oldVEIVr,VEIVr,false)!=0) then begin
      end;
    end;
  end;
  return;
end;
//cust - end

global //cust KB 04.12.2017
updating procedure AttachFileToVEVI(record VEIVVc VEIVr,string fnamep,Area a,boolean splitlines,boolean AutoVIfromVEIV)
begin
  Area tmpa,bina;
  string 100 fname;
//auto storing
  record VIVc VIr;

  fname = "tmp/" & fnamep;
  if (splitlines) then begin
    BreakIntoBase64Lines(a,tmpa);
    AreaBase64Decode(tmpa,bina);
  end else begin
    AreaBase64Decode(a,bina);
  end;
  CreateFile(fname);
  CloseFile;
  WriteAreaToFile(bina,fname,0);
//cust - begin: TS 2018/05, manuse lisamine ostuarvele
  if (nonblank(VEIVr.VISerNr)) then begin
    VIr.SerNr = VEIVr.VISerNr;
    if (ReadFirstMain(VIr,1,true)) then begin
      RecordLinkFile(fname,0,VIr,CurrentCompany);      
    end;
  end;
//cust - end
  LinkFileToVEIVRecord(VEIVr,fname);
//cust begin: TS 2018/04
  if (AutoVIfromVEIV) and (VEIVr.VISerNr<0) then begin
    DoCreateVIfromVEIV(VEIVr);
  end;
//cust - end
  return;
end;

//cust - begin: ts 2018/05
global
updating procedure AttachFileToCUIV(record IVVc IVr,string fnamep,Area a,boolean splitlines)
begin
  Area tmpa,bina;
  string 100 fname;

  fname = "tmp/" & fnamep;
  if (splitlines) then begin
    BreakIntoBase64Lines(a,tmpa);
    AreaBase64Decode(tmpa,bina);
  end else begin
    AreaBase64Decode(a,bina);
  end;
  CreateFile(fname);
  CloseFile;
  WriteAreaToFile(bina,fname,0);
  LinkFileToCUIVRecord(IVr,fname);
  return;
end;
//cust - end

//cust - begin: TS 2018/04
updating function string 20 CreateNewContact(record VEIVVc VEIVr)
begin
  record CUVc VEr;
  string 200 tstr;

  RecordNew(VEr);
  if (GetNextCustNr(tstr)) then begin
  end;
  VEr.Code = tstr;
  VEr.CUType = 0;
  VEr.VEType = 1;
  VEr.Name = VEIVr.VECompName;
  VEr.RegNr1 = VEIVr.VERegNr1;
  VEr.VATNr = VEIVr.VEVATNr;
  VEr.InvAddr0 = VEIVr.VEAddr0;
  VEr.InvAddr1 = VEIVr.VEAddr1;
  VEr.InvAddr2 = VEIVr.VEAddr2;
  VEr.InvAddr3 = "";
  VEr.InvAddr4 = "";
  VEr.DelAddr0 = "";
  VEr.DelAddr1 = "";
  VEr.DelAddr2 = "";
  VEr.DelAddr3 = "";
  VEr.DelAddr4 = "";
  VEr.Phone = VEIVr.VEPhone;
  VEr.Fax = "";
  VEr.eMail = VEIVr.VEeMail;
  VEr.ExportFlag = VEIVr.ExportFlag;
  //VEr.PayDeal = IVp.PayDeal;
  VEr.CountryCode = VEIVr.VECountryCode;
  VEr.LangCode = VEIVr.LangCode;
  VEr.BankAccount = VEIVr.VEBank2;
  if (RecordStore(VEr,false)) then begin end;
  CreateNewContact = VEr.Code;
  return;
end;
//cust - end

global
updating procedure ProcessReceived2Reply(string partner,area reply,string ourCustomerCode,var date td,var time t,Boolean AutoVIfromVEIV,Boolean VEIVwithPdf,var Integer cnt)
begin
  record VEIVVc VEIVr,oldVEIVr;
  row VEIVVc VEIVrw;
  record PDVc PDr;
  string 255 xkey,xmainkey,xsubkey,xsubkey2,xsubkey3,tstr,accstr,objstr,tdstr,InvoiceNr,InvoiceID;
  val tmpVal,v;
  Integer i,j,k,i0,rownr;
  Boolean testf,foundf,credf,checkvatf;
  Date tmpDate;
//cust + FITEK
  string 255 fname;
  area da;
  xml xdata;
//auto storing
//record VIVc VIr;
//string 255 fieldname;
//LongInt r;

  xdata = ParseXmlArea(reply);

  cnt = 0;
  if (partner == "FITEK2") then begin
    td = CurrentDate;
    t = CurrentTime;
    xkey = "soapenv:Envelope/soapenv:Body/ns:getBuyInvoiceResponse/ns:return/E_Invoice/Header/";
    //InvoiceNr = XmlGet(xdata,xkey & "FileId");//TS - 2017/12
    xmainkey = "soapenv:Envelope/soapenv:Body/ns:getBuyInvoiceResponse/ns:return";
  end;
  if (partner == "EARVEKESKUS2") then begin
    xmainkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/BuyInvoicesResponse";
    if (XmlNodeExists(xdata,xmainkey)) then begin
      tdstr = Left(XmlGetAttribute(xdata,xmainkey,"latestChange"),10);
      t = Right(XmlGetAttribute(xdata,xmainkey,"latestChange"),8);
      td.year = StringToLongInt(Left(tdstr,4));
      td.month = StringToInt(Mid(tdstr,5,2));
      td.day = StringToInt(Right(tdstr,2));
    end else begin
      td = CUrrentDate;
      t = CurrentTime;
    end;
  end;
  if (partner == "ENVOICE2") then begin
    xmainkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/BuyInvoiceExportResponse";
    if (XmlNodeExists(xdata,xmainkey)) then begin
      tdstr = Left(XmlGetAttribute(xdata,xmainkey,"latestChange"),10);
      t = mid(XmlGetAttribute(xdata,xmainkey,"latestChange"),11,8);
      t = AddSeconds(t,1);
      td.year = StringToLongInt(Left(tdstr,4));
      td.month = StringToInt(Mid(tdstr,5,2));
      td.day = StringToInt(Right(tdstr,2));
    end;
  end;
  //cust KB 08.11.2017 tsekk.ee
  if (partner == "TSEKKEE") then begin
    xmainkey = "TsekkRootElement";
  end;
  //cust end
  i = 0;
  while (XmlNodeExists(xdata,xmainkey & "/E_Invoice/Invoice[" & i & "]")) begin
    RecordNew(VEIVr);
    testf = false;
    xkey = xmainkey & "/E_Invoice/Invoice[" & i & "]";
    VEIVr.VESerNr = XmlGetAttribute(xdata,xkey,"invoiceId");
    if (partner == "ENVOICE2") then begin
      InvoiceID = XmlGetAttribute(xdata,xkey,"invoiceId");
      InvoiceID = Right(InvoiceID,len(InvoiceID)-7);
      InvoiceID = Left(InvoiceID,len(InvoiceID)-5);
      VEIVr.VESerNr = InvoiceID;
    end;
    xkey = xkey & "/";
    VEIVr.VersionNr = 10000;
//SellerParty
    xsubkey = xkey & "InvoiceParties/SellerParty/";
    VEIVr.VECode = XmlGet(xdata,xsubkey & "UniqueCode");
    VEIVr.VECompName = XmlGet(xdata,xsubkey & "Name");
    VEIVr.VERegNr1 = XmlGet(xdata,xsubkey & "RegNumber");
    VEIVr.VEVATNr = XmlGet(xdata,xsubkey & "VATRegNumber");
    xsubkey = xkey & "InvoiceParties/SellerParty/ContactData/";
    VEIVr.VEPhone = XmlGet(xdata,xsubkey & "PhoneNumber");
    VEIVr.VEeMail = XmlGet(xdata,xsubkey & "E-mailAddress");
    xsubkey = xkey & "InvoiceParties/SellerParty/ContactData/LegalAddress/";
    VEIVr.VEAddr0 = XmlGet(xdata,xsubkey & "PostalAddress1");
    VEIVr.VEAddr1 = XmlGet(xdata,xsubkey & "City");
    VEIVr.VEAddr2 = XmlGet(xdata,xsubkey & "PostalCode");
    VEIVr.VECountryCode = XmlGet(xdata,xsubkey & "Country");
    VEIVr.LangCode = XmlGet(xdata,xsubkey & "languageId");
    xsubkey = xkey & "InvoiceParties/SellerParty/";
    j = 0;
    if (XmlNodeExists(xdata,xsubkey & "AccountInfo[" & j & "]")) then begin
      xsubkey2 = xsubkey & "AccountInfo[" & j & "]/";
      VEIVr.VEBank2 = XmlGet(xdata,xsubkey2 & "IBAN");
      if (blank(VEIVr.VEBank2)) then begin
        VEIVr.VEBank2 = XmlGet(xdata,xsubkey2 & "AccountNumber");
      end;
      VEIVr.VEBank1 = XmlGet(xdata,xsubkey2 & "BankName");
    end;
//BuyerParty
    xsubkey = xkey & "InvoiceParties/BuyerParty/";
    VEIVr.RegNr1 = XmlGet(xdata,xsubkey & "RegNumber");
    VEIVr.VATNr = XmlGet(xdata,xsubkey & "VATRegNumber");
    xsubkey = xkey & "InvoiceParties/SellerParty/ContactData/";
    VEIVr.Phone = XmlGet(xdata,xsubkey & "PhoneNumber");
    xsubkey = xkey & "InvoiceParties/BuyerParty/ContactData/LegalAddress/";
    VEIVr.Addr0 = XmlGet(xdata,xsubkey & "PostalAddress1");
    VEIVr.Addr1 = XmlGet(xdata,xsubkey & "City");
    VEIVr.Addr2 = XmlGet(xdata,xsubkey & "PostalCode");
    xsubkey = xkey & "InvoiceParties/BuyerParty/ContactData/MailAddress/";//from company
    VEIVr.ShipAddr0 = XmlGet(xdata,xsubkey & "PostalAddress1");
    VEIVr.ShipAddr1 = XmlGet(xdata,xsubkey & "City");
    VEIVr.ShipAddr2 = XmlGet(xdata,xsubkey & "PostalCode");
    VEIVr.ShipAddr3 = XmlGet(xdata,xsubkey & "Country");
//InvoiceInformation
    xsubkey = xkey & "InvoiceInformation/Type";
    if (XmlGetAttribute(xdata,xsubkey,"type") == "CRE") then begin
      VEIVr.InvType = kInvoiceTypeCredit;
      credf = true;
    end;
    xsubkey = xkey & "InvoiceInformation/";
//cust - begin: TS 2016/06
    tstr = XmlGet(xdata,xsubkey & "ContractNumber");
    if (nonblank(tstr)) then begin
      VEIVr.OurOrdNr = StringToLongInt(tstr);
    end;
//cust - end
    //if (partner != "FITEK2") then begin
    InvoiceNr = XmlGet(xdata,xsubkey & "InvoiceNumber");
    //end;
    oldVEIVr.RegNr1 = VEIVr.RegNr1;
    oldVEIVr.VESerNr = VEIVr.VESerNr;
    oldVEIVr.InvoiceNr = InvoiceNr;
    if (ReadLastKey("RegNr1",oldVEIVr,3,true)) then begin
      testf = true;
    end;
    VEIVr.InvComment = XmlGet(xdata,xsubkey & "InvoiceContentText");
    if (blank(VEIVr.InvComment)) then begin
      VEIVr.InvComment = XmlGet(xdata,xsubkey & "comment");
    end;
    VEIVr.CalcFinRef = XmlGet(xdata,xsubkey & "PaymentReferenceNumber");
    VEIVr.InvDate = XmlGetDate(xdata,xsubkey & "InvoiceDate","YYYY-MM-DD");
    VEIVr.PayDate = XmlGetDate(xdata,xsubkey & "DueDate","YYYY-MM-DD");
    VEIVr.TransDate = VEIVr.InvDate;
    VEIVr.InvoiceNr = InvoiceNr;
    if (VEIVr.InvType == kInvoiceTypeCredit) then begin
      foundf = true;
      PDr.Code = "";
      ResetLoop(PDr);
      while (LoopMain(PDr,1,foundf)) begin
        if ((PDr.PDType)==3) then begin //credit note
          foundf = false;
          VEIVr.PayDeal = PDr.Code;
        end;  
      end;  
    end else begin
      VEIVr.PayDeal = DateDiff(VEIVr.PayDate,VEIVr.InvDate);
    end;
    VEIVr.IntCode = XmlGet(xdata,xsubkey & "FineRatePerDay");//is it per day?
    j = 0;
    while (XmlNodeExists(xdata,xsubkey & "Extension[" & j & "]")) begin
      xsubkey2 = xsubkey & "Extension[" & j & "]";
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="eakTransactionDate") then begin
        VEIVr.TransDate = XmlGetDate(xdata,xsubkey2 & "/InformationContent","YYYY-MM-DD");
      end;
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="eakBuyInvoiceComment") then begin
        VEIVr.InvComment = XmlGet(xdata,xsubkey2 & "InformationContent");
      end;
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="eakPurchaseOrderRef") then begin
        VEIVr.OurOrdNr = XmlGet(xdata,xsubkey2 & "/InformationContent");
      end;
//Fitek
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="PurchaseOrder") then begin
        VEIVr.OurOrdNr = XmlGet(xdata,xsubkey2 & "/InformationContent");
      end;
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="eakInvoiceRegion") then begin
        VEIVr.Region = XmlGet(xdata,xsubkey2 & "/InformationContent");  
      end;
//Envoice
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="order") then begin
        VEIVr.OurOrdNr = XmlGet(xdata,xsubkey2 & "/InformationContent");
      end;
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="PaymentMethodCode") then begin
        VEIVr.PayDeal = XmlGet(xdata,xsubkey2 & "/InformationContent");
      end;
      j = j + 1;
    end;
    VEIVr.OurPayDeal = VEIVr.PayDeal;
//InvoiceSumGroup
    checkvatf = true;
    xsubkey = xkey & "InvoiceSumGroup/";
    k = 0;
    while (XmlNodeExists(xdata,xkey & "InvoiceSumGroup/VAT[" & k & "]")) begin
      xsubkey = xkey & "InvoiceSumGroup/VAT[" & k & "]/";
      v = XmlGetVal(xdata,xsubkey & "SumBeforeVAT",M4Val,",","");
      if (credf and v<0) then begin
        v = -v;
      end;
      VEIVr.Sum1 = VEIVr.Sum1 + v;
      v = XmlGetVal(xdata,xsubkey & "VATSum",M4Val,",","");
      if (credf and v<0) then begin
        v = -v;
      end;
      VEIVr.Sum3 = VEIVr.Sum3 + v;
      k = k + 1;
      checkvatf = false;
    end;
    xsubkey = xkey & "InvoiceSumGroup/";
    VEIVr.VETotQty = XmlGetVal(xdata,xsubkey & "InvoiceItemTotalAmount",M4Val,",","");
    v = XmlGetVal(xdata,xsubkey & "TotalSum",M4Val,",","");
    if (credf and v<0) then begin
      v = -v;
    end;
    VEIVr.Sum4 = v;
    VEIVr.CurncyCode = XmlGet(xdata,xsubkey & "Currency");
//Fitek, Envoice, another place
    j = 0;
    while (XmlNodeExists(xdata,xsubkey & "Extension[" & j & "]")) begin
      xsubkey2 = xsubkey & "Extension[" & j & "]";
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="eakTransactionDate") then begin
        VEIVr.TransDate = XmlGetDate(xdata,xsubkey2 & "/InformationContent","YYYY-MM-DD");
      end;
      if (XmlGetAttribute(xdata,xsubkey2,"extensionId")=="transactionDate") then begin
        VEIVr.TransDate = XmlGetDate(xdata,xsubkey2 & "/InformationContent","YYYY-MM-DD");
      end;
      j = j + 1;
    end;
//InvoiceItem
    rownr = 0;
    i0 = 0;
    while (XmlNodeExists(xdata,xkey & "InvoiceItem/InvoiceItemGroup[" & i0 & "]")) begin
      xsubkey = xkey & "InvoiceItem/InvoiceItemGroup[" & i0 & "]/";
      j = 0;
      while (XmlNodeExists(xdata,xsubkey & "ItemEntry[" & j & "]")) begin
        ClearRow(VEIVr,VEIVrw,1);
        xsubkey2 = xsubkey & "ItemEntry[" & j & "]/";
        VEIVrw.VEArtCode = XmlGet(xdata,xsubkey2 & "SellerProductId");
        VEIVrw.ArtCode = XmlGet(xdata,xsubkey2 & "BuyerProductId");
        VEIVrw.VESpec = XmlGet(xdata,xsubkey2 & "Description");
        VEIVrw.VESerialNr = XmlGet(xdata,xsubkey2 & "SerialNumber");
        k = 0;
        objstr = "";
        while (XmlNodeExists(xdata,xsubkey2 & "Accounting/JournalEntry[" & k & "]")) begin
          xsubkey3 = xsubkey2 & "Accounting/JournalEntry[" & k & "]/";
          accstr = XmlGet(xdata,xsubkey3 & "GeneralLedger");
          tstr = XmlGet(xdata,xsubkey3 & "CostObjective");
          if (nonblank(tstr) and nonblank(objstr)) then begin
            objstr = objstr & ",";
          end;
          objstr = objstr & tstr;  
          k = k + 1;
        end;
        VEIVrw.CostAcc = accstr;
        VEIVrw.Objects = objstr;
        k = 0;
        while (XmlNodeExists(xdata,xsubkey2 & "ItemReserve[" & k & "]")) begin
          xsubkey3 = xsubkey2 & "ItemReserve[" & k & "]";
          if (XmlGetAttribute(xdata,xsubkey3,"extensionId")=="eakInvoiceItemId") then begin
            VEIVrw.VEArtCode = XmlGet(xdata,xsubkey3 & "/InformationContent");
          end;
          if (XmlGetAttribute(xdata,xsubkey3,"extensionId")=="eakOppositeAccount") then begin
            accstr = XmlGet(xdata,xsubkey3 & "/InformationContent");  
          end;
         //Omniva
          if (XmlGetAttribute(xdata,xsubkey3,"extensionId")=="eakVatCode") then begin
            VEIVrw.VATCode = XmlGet(xdata,xsubkey3 & "/InformationContent");
          end;
         //Fitek
          if (XmlGetAttribute(xdata,xsubkey3,"extensionId")=="ocVatCode") then begin
            VEIVrw.VATCode = XmlGet(xdata,xsubkey3 & "/InformationContent");
          end;
          if (XmlGetAttribute(xdata,xsubkey3,"extensionId")=="vatCode") then begin
            VEIVrw.VATCode = XmlGet(xdata,xsubkey3 & "/InformationContent");
          end;
          k = k + 1;
        end;
        if (nonblank(accstr)) then begin
          VEIVr.CostAcc = accstr;
        end;     
        VEIVrw.VEUnitCode = XmlGet(xdata,xsubkey2 & "ItemUnit");
        VEIVrw.VEQuant = XmlGetVal(xdata,xsubkey2 & "ItemAmount",M4Val,",","");
        v = XmlGetVal(xdata,xsubkey2 & "ItemPrice",M4Val,",","");
        if (credf and v<0) then begin
          v = -v;
        end;
        VEIVrw.VEPrice = v;  
        v = XmlGetVal(xdata,xsubkey2 & "ItemSum",M4Val,",","");
        if (credf and v<0) then begin
          v = -v;
        end;
        VEIVrw.VESum = v;  
        xsubkey3 = xsubkey2 & "Addition";
        if (XmlGetAttribute(xdata,xsubkey3,"addCode") == "DSC") then begin
          tmpVal = XmlGetVal(xdata,xsubkey3 & "/AddRate",M4Val,",","");//-15
          if (tmpVal <> 0) then begin
            tmpVal = -tmpVal;
          end;
          VEIVrw.VEvRebate = tmpVal;
        end;
        xsubkey2 = xsubkey & "ItemEntry[" & j & "]/VAT/";
        if(nonblank(XmlGetVal(xdata,xsubkey2 & "SumBeforeVAT",M4Val,",",""))) then begin //cust fix KB 05.12.2017 this is overwriting ItemSum value otherwise
        v = XmlGetVal(xdata,xsubkey2 & "SumBeforeVAT",M4Val,",","");
        if (credf and v<0) then begin
          v = -v;
        end;
        VEIVrw.VESum = v;
        end;//cust fix KB 05.12.2017
        if (checkvatf) then begin
          VEIVr.Sum1 = VEIVr.Sum1 + v;
        end;
        VEIVrw.VEVATPrc = XmlGetVal(xdata,xsubkey2 & "VATRate",M4UVal,",","");//20
        v = XmlGetVal(xdata,xsubkey2 & "VATSum",M4Val,",","");
        if (credf and v<0) then begin
          v = -v;
        end;
        VEIVrw.VEVATSum = v;
        if (checkvatf) then begin
          VEIVr.Sum3 = VEIVr.Sum3 + v;
        end;
        xsubkey2 = xsubkey & "ItemEntry[" & j & "]/";
        MatRowPut(VEIVr,rownr,VEIVrw);
        rownr = rownr + 1;
        j = j + 1;
      end;
      i0 = i0 + 1;
    end;
    xkey = xmainkey & "/E_Invoice/Invoice[" & i & "]/";
//PaymentInfo
    xsubkey = xkey & "PaymentInfo/";
    VEIVr.VEPayToAccount = XmlGet(xdata,xsubkey & "PayToAccount");
    VEIVr.VEPayToName = XmlGet(xdata,xsubkey & "PayToName");
    PutOurDataToVEIV(VEIVr);
    if (testf) then begin
      VEIVr.VersionNr = oldVEIVr.VersionNr + 1;
    end;
    if (VEIVr.VersionNr<0) then begin VEIVr.VersionNr = 0; end;
    VEIVr.SerNr = NextSerNr("VEIVVc",VEIVr.TransDate,-1,false,"");
    if (VEIVr.SerNr>0) then begin
//cust begin: TS 2018/04
      if (AutoVIfromVEIV) then begin
        if (blank(VEIVr.VECode)) then begin
          VEIVr.VECode = CreateNewContact(VEIVr);
        end;
      end;
//cust - end
      if (RecordStore(VEIVr,false)) then begin 
      end;
//cust begin: TS 2018/04
      if (AutoVIfromVEIV) and (VEIVwithPdf==false) then begin
        DoCreateVIfromVEIV(VEIVr);
      end;
//cust - end
      cnt = cnt + 1;
//Fitek attachment
      if (partner == "FITEK2") then begin
        if (XmlNodeExists(xdata,xkey & "AdditionalInformation")) begin
          if (XmlGetAttribute(xdata,xkey & "AdditionalInformation","extensionId")=="invoicePDFFormat") then begin
            XmlGetArea(xdata,xkey & "AdditionalInformation/CustomContent/any/Content",da);
            fname = "invoice" & VEIVr.InvoiceNr & ".pdf";
            AttachFileToVEVI(VEIVr,fname,da,false,AutoVIfromVEIV);
            SetAreaZeroSize(da);
          end;
        end;
      end;
    end;
    i = i + 1;
  end;
  return;
end;

global
procedure ProcessReceived2PdfReply(string partner,xml xdata,LongInt serrange,var Integer nextAttachmentIndex,string VEIVNr,var Integer TotalCnt,boolean AutoVIfromVEIV)
begin
  record VEIVVc VEIVr,oldVEIVr;
  string 255 xkey,xsubkey,regnr,fname,type;
  LongInt InvoiceID;
  Integer i;
  Boolean testf,TrHs;
  Area da;
 
  TotalCnt = 0;
  if (ReadFirstMain(VEIVr,1,false)) then begin 
    regnr = VEIVr.RegNr1;
  end;
  if (partner == "FITEK2") then begin
    xkey = "soapenv:Envelope/soapenv:Body/ns:GetAttachmentResponse/ns:return/Attachments/attachment";
    VEIVr.SerNr = VEIVNr;
    if (ReadFirstMain(VEIVr,1,true)) then begin end;
    i = 0;
    while (XmlNodeExists(xdata,xkey & "[" & i & "]")) begin
      TotalCnt = TotalCnt + 1;
      testf = false;
      xsubkey = xkey & "[" & i & "]";
      fname = XmlGetAttribute(xdata,xsubkey,"name");
      type = XmlGetAttribute(xdata,xsubkey,"type");
      if (type != "xml") then begin
        if (blank(fname)) then begin
          fname = "invoice" & VEIVr.InvoiceNr & "." & type;
        end;
        XmlGetArea(xdata,xsubkey,da);
        qupdating.AttachFileToVEVI(VEIVr,fname,da,false,AutoVIfromVEIV);
        SetAreaZeroSize(da);
      end;
      i = i + 1;
    end;
  end else begin
    if (partner == "EARVEKESKUS2") then begin
      xkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/erp:InvoiceAttachmentResponse";
    end;
    if (partner == "ENVOICE2") then begin
      xkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/InvoiceAttachmentResponse";
    end;
    nextAttachmentIndex = StringToInt(XmlGetAttribute(xdata,xkey,"nextAttachmentIndex"));
    xkey = xkey & "/InvoiceAttachment";
    while (XmlNodeExists(xdata,xkey & "[" & i & "]")) begin
      TotalCnt = TotalCnt + 1;
      testf = false;
      xsubkey = xkey & "[" & i & "]";
      InvoiceID = StringToLongInt(XmlGetAttribute(xdata,xsubkey,"invoiceId"));
      fname = XmlGetAttribute(xdata,xsubkey,"fileName");
      if (nonblank(InvoiceID)) then begin
        VEIVr.RegNr1 = regnr;
        VEIVr.VESerNr = InvoiceID;
        TrHs = true;
        ResetLoop(VEIVr);
        while (LoopBackKey("RegNr1",VEIVr,2,TrHs)) begin
          if (VEIVr.VESerNr == InvoiceID) and (VEIVr.SerNr >= serrange) then begin
            XmlGetArea(xdata,xsubkey & "/AttachmentContent",da);
            qupdating.AttachFileToVEVI(VEIVr,fname,da,true,AutoVIfromVEIV);
            SetAreaZeroSize(da);
            TrHs = false;
          end;
        end;
      end;
      i = i + 1;
    end;
  end;
  return;
end;

procedure ProcessReceivedIVPdfReply(string partner,xml xdata,LongInt serrange,var Integer nextAttachmentIndex,string CUIVNr,var Integer TotalCnt)
begin
  record CUIVVc CUIVr;
  record IVVc IVr;
  string 255 xkey,xsubkey,regnr,fname,type;
  string 30 InvoiceID;
  Integer i;
  Area da;
 
  TotalCnt = 0;
  if (partner == "FITEK3") then begin
    xkey = "soapenv:Envelope/soapenv:Body/ns:GetAttachmentResponse/ns:return/Attachments/attachment";
    CUIVr.SerNr = CUIVNr;
    if (ReadFirstMain(CUIVr,1,true)) then begin end;
    IVr.SerNr = CUIVr.IVSerNr;
    if (ReadFirstMain(IVr,1,true)) then begin
      i = 0;
      while (XmlNodeExists(xdata,xkey & "[" & i & "]")) begin
        TotalCnt = TotalCnt + 1;
        xsubkey = xkey & "[" & i & "]";
        fname = XmlGetAttribute(xdata,xsubkey,"name");
        type = XmlGetAttribute(xdata,xsubkey,"type");
        if (type != "xml") then begin
          if (blank(fname)) then begin
            fname = "invoice" & CUIVr.InvoiceNr & "." & type;
          end;
          XmlGetArea(xdata,xsubkey,da);
          qupdating.AttachFileToCUIV(IVr,fname,da,false);
          SetAreaZeroSize(da);
        end;
        i = i + 1;
      end;
    end;
  end else begin
    if (partner == "EARVEKESKUS3") then begin
      xkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/erp:InvoiceAttachmentResponse";
    end;
    if (partner == "ENVOICE3") then begin
      xkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/InvoiceAttachmentResponse";
    end;
    nextAttachmentIndex = StringToInt(XmlGetAttribute(xdata,xkey,"nextAttachmentIndex"));
    xkey = xkey & "/InvoiceAttachment";
    while (XmlNodeExists(xdata,xkey & "[" & i & "]")) begin
      TotalCnt = TotalCnt + 1;
      xsubkey = xkey & "[" & i & "]";
      InvoiceID = XmlGetAttribute(xdata,xsubkey,"invoiceId");
      fname = XmlGetAttribute(xdata,xsubkey,"fileName");
      if (nonblank(InvoiceID)) then begin
        CUIVr.InvoiceId = InvoiceID;
        if (ReadLastKey("InvoiceId",CUIVr,1,true)) begin
          IVr.SerNr = CUIVr.IVSerNr;
          if (ReadFirstMain(IVr,1,true)) then begin
            //if (VEIVr.VESerNr == InvoiceID) and (VEIVr.SerNr >= serrange) then begin//always attach to latest with InvoiceId
            XmlGetArea(xdata,xsubkey & "/AttachmentContent",da);
            qupdating.AttachFileToCUIV(IVr,fname,da,true);
            SetAreaZeroSize(da);
          end;
        end;
      end;
      i = i + 1;
    end;
  end;
  return;
end;

global
updating procedure ChangeERecordStatus(array LongInt aRecordNr,Integer cnt,Integer status)
begin
  record ERecordStatusVc ERecordStatusr,oldERecordStatusr;
  Integer i;

  for (i=0; i<cnt; i=i+1) begin
    ERecordStatusr.SerNr = aRecordNr[i];
    if (ReadFirstMain(ERecordStatusr,1,true)) then begin
      RecordCopy(oldERecordStatusr,ERecordStatusr);
      ERecordStatusr.Status = status;
      if (RecordUpdate(oldERecordStatusr,ERecordStatusr,false)) then begin end;    
    end;
  end;
  return;
end;

procedure DoSendSupplierEstEInvoiceStatus(record EInvoiceBlock EIb,string service,boolean checkflag,var xml xdata)
begin
  record ERecordStatusVc ERecordStatusr,oldERecordStatusr;
  Boolean res,foundf,testf;
  array LongInt aRecordNr;
  Integer cnt;
  Area a;

  cnt = 0;
  ERecordStatusr.FileName = "VEIVVc";
  ERecordStatusr.Status = 1;
  foundf = true;
  while (LoopKey("FileNameStatus",ERecordStatusr,2,foundf)) begin
    if (ERecordStatusr.FileName <> "VEIVVc") then begin
      foundf = false;
    end;
    if (ERecordStatusr.Status <> 1) then begin
      foundf = false;
    end;
    testf = true;
    if (ERecordStatusr.CompNr <> CurrentCompany) then begin
      testf = false;
    end;
    if foundf and testf then begin
      AddStatusToeArveKeskusReply(ERecordStatusr,a);
      aRecordNr[cnt] = ERecordStatusr.SerNr;
      cnt = cnt + 1;
      if (cnt >= 1000) then begin
        foundf = false;
      end;
    end;
  end;
  if (cnt > 0) then begin
    SendSupplierEstEInvoiceStatus(EIb,service,checkflag,a,xdata,res);
    if (res) then begin
      queued.ChangeERecordStatus(aRecordNr,cnt,2);
    end;
  end;
  return;
end;

procedure DoSendSupplierRejEstEInvoiceStatus(record EInvoiceBlock EIb,string service,boolean checkflag,var xml xdata)
begin
  record ERecordStatusVc ERecordStatusr;
  string 100 prevcontact,user,comment,prevregnr;
  Integer pos;
  Boolean foundf,testf;
  array LongInt aRecordNr;
  Integer i,cnt;
  Area a;

  cnt = 0;
  ERecordStatusr.FileName = "VEIVVc";
  ERecordStatusr.Status = 0;
  foundf = true;
  while (LoopKey("FileNameStatus",ERecordStatusr,2,foundf)) begin
    if (ERecordStatusr.FileName <> "VEIVVc") then begin
      foundf = false;
    end;
    if (ERecordStatusr.Status <> 0) then begin
      foundf = false;
    end;
    testf = true;
    if (ERecordStatusr.CompNr <> CurrentCompany) then begin
      testf = false;
    end;
    if foundf and testf then begin
      pos = 0;
      ExtractObjWithSeparator(";",ERecordStatusr.Comment,true,pos,comment);
      ExtractObjWithSeparator(";",ERecordStatusr.Comment,true,pos,user);
      if ((i>0) and ((user <> prevcontact) or (prevregnr <> ERecordStatusr.RegNr1))) then begin
        SendSupplierRejEstEInvoiceStatus(EIb,service,checkflag,a,xdata,prevregnr,prevcontact);
        SetAreaZeroSize(a);
        cnt = 0;
      end;
      AddRejStatusToeArveKeskusReply(ERecordStatusr,comment,a);
      prevcontact = user;
      prevregnr = ERecordStatusr.RegNr1;
      aRecordNr[cnt] = ERecordStatusr.SerNr;
      cnt = cnt + 1;
      if (cnt >= 1000) then begin
        foundf = false;
      end;
    end;
  end;
  if (i>0) then begin
    SendSupplierRejEstEInvoiceStatus(EIb,service,checkflag,a,xdata,prevregnr,prevcontact);
  end;
  return;
end;

procedure GetSupplierEstEInvoice(record EInvoiceBlock EIb,record CYBlock CYb,string service,string partner,string CustomerCode,Integer type,string status,var date td,var time t,var Integer cnt) //ts - 2018/04, added parameter for e-service statistic
begin
  area reply;

  SendSupplierInvoiceRequest(EIb,CYb,service,partner,reply,status,td,t);
  qupdating.ProcessReceived2Reply(partner,reply,CustomerCode,td,t,(EIb.AutoVIfromVEIV==1),(EIb.StopInEstEInvPdf==0),cnt);
  return;
end;

global
procedure ReceiveEstEInvoiceIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf,stopidletaskf;
  Integer i,cnt,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  record CYBlock CYb;
  record InternetEnablerBlock IEb;
  Date td;
  Time t;

  SCr.Code = "RECEIVEESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin
    stopidletaskf = true;
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              stopidletaskf = false;
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine3;
            end;
          end;
          if (EIb.StopInEstEInvoices == 1) then begin
            if (EIb.InEstEInvoices==1) then begin 
              stopidletaskf = false;
            end;
            goto LNextCompanyLine3;
          end;
          testf = true;
          if (EIb.InEstEInvoices==0) then begin 
            testf = false;
          end;
          if (testf) then begin
            BlockLoad(IEb);
            BlockLoad(CYb);
            GetSupplierEstEInvoice(EIb,CYb,SCr.Code,EIb.SuppInvoicePartner,IEb.CustomerCode,1,"",td,t,cnt);
            qupdating.UpdateReceiveEstEInvoice(EIb,td,t,CurrentDate,CurrentTime);
            //if (cnt > 0) then begin //ts - 2018/04, for now we count also request with 0 response
              queued.StoreCUServiceUsage(CurrentDate,CYb.OrgNr,1,EIb.SuppInvoicePartner,1,cnt);//nr of invoices received //cust, ts - 2018/04
            //end;
            stopidletaskf = false;
          end;
        end;
      end;
LNextCompanyLine3:;
    end;
    ResetCompany(oldcomp);
    if (stopidletaskf) then begin
      ReceiveEstEInvoiceIdleTaskRemoveIdleTask2;
    end;
  end;
LReceiveEstEInvoiceIdleTask:;
  return;
end;

procedure GetSupplierEstEInvoicePdf(record EInvoiceBlock EIb,string service,string partner,var LongInt LastChecked)
begin
  record VEIVVc VEIVr;
  array LongInt aSerNr;
  array string 30 aInvoiceId;
  string 255 tstr;
  Integer aCnt,maxcnt,startIndex,nextAttacmentIndex,TotalCnt;
  Boolean TrHs,test,errf;
  xml xdata;
  area reply;

  LastChecked = EIb.LastInEstEInvStatusSerial;
  maxcnt = EIb.CntInEstEInvPdf;
  if (maxcnt <= 0) then begin
    maxcnt = 10;
  end;
  aCnt = 0;
  TrHs = true;
  VEIVr.SerNr = EIb.LastInEstEInvStatusSerial;
  VEIVr.SerNr = VEIVr.SerNr + 1;
  while (LoopMain(VEIVr,1,TrHs)) begin
    if (TrHs) then begin
      aSerNr[aCnt] = VEIVr.SerNr;
      aInvoiceId[aCnt] = VEIVr.VESerNr;
      aCnt = aCnt + 1;
      if (aCnt >= maxcnt) then begin
        TrHs = false;
      end;
    end;
  end;
  if (aCnt > 0) then begin
    startIndex = 1;
L12:;
    SendSupplierInvoicePdfRequest(EIb,service,partner,(EIb.DebugFlag==1),reply,xdata,aCnt,aInvoiceId,startIndex);
    if (partner == "FITEK2") or (partner == "FITEK3") then begin
    end else begin
      if (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring")) then begin
        tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode");
        errf = true;
        if (LastInRange(tstr,5)=="71") then begin      
          LastChecked = aSerNr[aCnt-1];
        end;
      end;
    end;
    if (errf==false) then begin
      ProcessReceived2PdfReply(partner,xdata,EIb.LastInEstEInvStatusSerial,nextAttacmentIndex,aSerNr[0],TotalCnt,(EIb.AutoVIfromVEIV==1));
      if (nextAttacmentIndex >= (startIndex + TotalCnt)) then begin
        startIndex = nextAttacmentIndex;
        goto L12;
      end;
      LastChecked = aSerNr[aCnt-1];
    end;
  end;
  return;
end;

//cust - begin: ts 2018/05
global
procedure GetSingleSupplierEstEInvoicePdf(record VEIVVc VEIVr,var Boolean errf)
begin
  record ServiceCacheVc SCr;
  record EInvoiceBlock EIb;
  array LongInt aSerNr;
  array string 30 aInvoiceId;
  string 255 tstr,partner;
  Integer aCnt,startIndex,nextAttacmentIndex,TotalCnt;
  Boolean found;
  xml xdata;
  area reply;

  SCr.Code = "RECEIVEESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    BlockLoad(EIb);
    partner = EIb.SuppInvoicePartner;
    aSerNr[0] = VEIVr.SerNr;
    aInvoiceId[0] = VEIVr.VESerNr;
    aCnt = 1;
    startIndex = 1;
    SendSupplierInvoicePdfRequest(EIb,SCr.Code,partner,(EIb.DebugFlag==1),reply,xdata,aCnt,aInvoiceId,startIndex);
    if (partner == "FITEK2") or (partner == "FITEK3") then begin
    end else begin
      if (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring")) then begin
        tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode");
        errf = true;
      end;
    end;
    if (errf==false) then begin
      ProcessReceived2PdfReply(partner,xdata,VEIVr.SerNr,nextAttacmentIndex,aSerNr[0],TotalCnt,(EIb.AutoVIfromVEIV==1));
    end;
  end;
  return;
end;
//cust - end

global
procedure ReceiveEstEInvoicePdfIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf;
  Integer i,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  LongInt LastChecked;

  SCr.Code = "RECEIVEESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine4;
            end;
          end;
          if (EIb.StopInEstEInvPdf == 1) then begin
            goto LNextCompanyLine4;
          end;
          if (EIb.StopInEstEInvoices == 1) then begin
            goto LNextCompanyLine4;
          end;
          testf = true;
          if (EIb.InEstEInvoices==0) then begin 
            testf = false;
          end;
          if (testf) then begin
            GetSupplierEstEInvoicePdf(EIb,SCr.Code,EIb.SuppInvoicePartner,LastChecked);
            if (EIb.LastInEstEInvStatusSerial <> LastChecked) then begin
              qupdating.UpdateReceiveEstEInvoicePdf(EIb,LastChecked);
            end;
          end;
        end;
      end;
LNextCompanyLine4:;
    end;
    ResetCompany(oldcomp);
  end;
LReceiveEstEInvoiceIdleTask:;
  return;
end;

global
procedure SendEstSupEInvoiceIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf;
  Integer i,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  xml xdata;

  SCr.Code = "RECEIVEESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine5;
            end;
          end;
          if (EIb.StopInEstEInvoices == 1) then begin
            goto LNextCompanyLine5;
          end;
          testf = true;
          if (EIb.InEstEInvoices==0) then begin 
            testf = false;
          end;
          if (EIb.SuppInvoicePartner=="FITEK2") then begin 
            testf = false;
          end;
          if (testf) then begin
            DoSendSupplierEstEInvoiceStatus(EIb,SCr.Code,(EIb.DebugFlag==1),xdata);
            DoSendSupplierRejEstEInvoiceStatus(EIb,SCr.Code,(EIb.DebugFlag==1),xdata);
            qupdating.UpdateSendEstSupEInvoice(EIb,CurrentDate,CurrentTime);
          end;
        end;
      end;
LNextCompanyLine5:;
    end;
    ResetCompany(oldcomp);
  end;
LSendEstSupEInvoiceIdleTask:;
  return;
end;

//cust - begin: ts 2018/05
updating procedure StoreCUIVVcRecord(string InvoiceId,string InvoiceNr,LongInt IVSerNr)
begin
  record CUIVVc CUIVr;
  Integer version;
  
  version = 0;
  CUIVr.InvoiceId = InvoiceId;
  if (ReadLastKey("InvoiceId",CUIVr,1,true)) then begin
    version = CUIVr.VersionNr + 1;
  end;
  RecordNew(CUIVr);
  CUIVr.InvoiceId = InvoiceId;
  CUIVr.InvoiceNr = InvoiceNr;
  CUIVr.IVSerNr = IVSerNr;
  CUIVr.VersionNr = version;
  CUIVr.SerNr = NextSerNr("CUIVVc",CurrentDate,-1,false,"");
  RecordStore(CUIVr,false);
  return;
end;
//cust - end

global
updating procedure ProcessReceivedInvoiceReply(string partner,area reply,string ourCustomerCode,var date td,var time t,var Integer cnt)
begin
  string 255 xmainkey,tdstr,InvoiceId,InvoiceNr;
  record CYBlock CYBl;
  LongInt IVSerNr;
  Integer i;
  xml xdata;
  
  xdata = ParseXmlArea(reply);

  xmainkey = "SOAP-ENV:Envelope/SOAP-ENV:Body/SaleInvoiceExportResponse";
  if (XmlNodeExists(xdata,xmainkey)) then begin
    tdstr = Left(XmlGetAttribute(xdata,xmainkey,"latestChange"),10);
    t = mid(XmlGetAttribute(xdata,xmainkey,"latestChange"),11,8);
    t = AddSeconds(t,1);
    td.year = StringToLongInt(Left(tdstr,4));
    td.month = StringToInt(Mid(tdstr,5,2));
    td.day = StringToInt(Right(tdstr,2));
  end;
  i = 0;
  while (XmlNodeExists(xdata,xmainkey & "/E_Invoice/Invoice[" & i & "]")) begin
    if (EArveXML3In(xdata,xmainkey & "/",i,IVSerNr)) then begin
      InvoiceId = XmlGetAttribute(xdata,xmainkey & "/E_Invoice/Invoice[" & i & "]","invoiceId");
      InvoiceNr = XmlGet(xdata,xmainkey & "/E_Invoice/Invoice[" & i & "]/InvoiceInformation/InvoiceNumber");        
      StoreCUIVVcRecord(InvoiceId,InvoiceNr,IVSerNr);
    end;
    i = i + 1;
  end;
  cnt = i;
  return;
end;

procedure SendInvoiceRequest(record EInvoiceBlock EIb,string service,string partner,var area reply,string status,var date SendDate,var time SendTime)
begin
  record VEIVVc VEIVr;
  string 100 param,tstr;
  area a;
  xml xdata;

  SendDate = EIb.LastInIVEstEInvoicesDate;
  SendTime = EIb.LastInIVEstEInvoicesTime;
  if (blankdate(SendDate)) then begin
    SendDate = AddDay(CurrentDate,-GetDay(CurrentDate)+1);
    SendTime = AddHours(CurrentTime,-3);
    if (SendTime.hour >= 21) and (SendTime.hour <= 23) then begin
      SendDate = AddDay(SendDate,-1);
    end;
  end;
  SetAreaZeroSize(a);
  tstr = datetostring(SendDate,"YYYY-MM-DD") & "T" & TimeToString(SendTime,"HH",false) & ":" & TimeToString(SendTime,"MM",false) & ":" & TimeToString(SendTime,"SS",false) & "+00:00";
  EstEInvBuildString2("since",tstr,param);  
  DoSendRequestToPartnerWithArea(a,reply,xdata,service,partner,"SaleInvoiceExport",param,EIb.PrivateKey,(EIb.DebugFlag==1));
  return;
end;

procedure GetEstEInvoice(record EInvoiceBlock EIb,string service,string partner,string CustomerCode,Integer type,string status,var date td,var time t,var integer cnt) //ts - 2018/04, added parameter for e-service statistic
begin
  area reply;

  SendInvoiceRequest(EIb,service,partner,reply,status,td,t);
  qupdating.ProcessReceivedInvoiceReply(partner,reply,CustomerCode,td,t,cnt);//ts - 2018/04, added parameter for e-service statistic
  return;
end;

global
updating procedure UpdateLastInIVEstEInvSerial(record EInvoiceBlock EIb,LongInt LastChecked)
begin
  EIb.LastInIVEstEInvSerial = LastChecked;
  BlockStore(EIb);
  return;
end;

global
procedure GetEstEInvoiceIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf,stopidletaskf;
  Integer i,cnt,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  record CYBlock CYb;
  record InternetEnablerBlock IEb;
  Date td;
  Time t;

  SCr.Code = "GETESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin
    stopidletaskf = true;
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              stopidletaskf = false;
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine6;
            end;
          end;
          if (EIb.StopInIVEstEInvoices == 1) then begin
            stopidletaskf = false;
            goto LNextCompanyLine6;
          end;
          testf = true;
          if (EIb.InIVEstEInvoices==0) then begin 
            testf = false;
          end;
          if (EIb.EInInvoicePartner!="ENVOICE3") then begin//ts - 2018/04, Partner Envoice doesn't support this request
            testf = false;
          end;
          if (testf) then begin
            BlockLoad(IEb);
            BlockLoad(CYb);
            GetEstEInvoice(EIb,SCr.Code,EIb.EInInvoicePartner,IEb.CustomerCode,1,"",td,t,cnt); //ts - 2018/04, added parameter for e-service statistic
            qupdating.UpdateGetEstEInvoice(EIb,td,t,CurrentDate,CurrentTime);
            //if (cnt > 0) then begin//ts - 2018, for now we count also request with 0 response
              queued.StoreCUServiceUsage(CurrentDate,CYb.OrgNr,12,EIb.EInInvoicePartner,1,cnt);//nr of invoices received //cust, ts - 2018/04
            //end;
            stopidletaskf = false;
          end;
        end;
      end;
LNextCompanyLine6:;
    end;
    ResetCompany(oldcomp);
    if (stopidletaskf) then begin
      GetEstEInvoiceIdleTaskRemoveIdleTask;
    end;
  end;
LReceive2EstEInvoiceIdleTask:;
  return;
end;

procedure DoGetEstEInvoicePdf(record EInvoiceBlock EIb,string service,string partner,var LongInt LastChecked)
begin
  record CUIVVc CUIVr;
  xml xdata;
  Boolean TrHs,test,errf;
  Integer aCnt,maxcnt,nextAttacmentIndex,TotalCnt,startIndex;
  array LongInt aSerNr;
  array string 30 aInvoiceId;
  area reply;
  string 255 tstr;

  LastChecked = EIb.LastInIVEstEInvSerial;
  maxcnt = EIb.CntInIVEstEInvPdf;
  if (maxcnt <= 0) then begin
    maxcnt = 10;
  end;
  aCnt = 0;
  TrHs = true;
  CUIVr.SerNr = EIb.LastInIVEstEInvSerial;
  CUIVr.SerNr = CUIVr.SerNr + 1;
  while (LoopMain(CUIVr,1,TrHs)) begin
    if (TrHs) then begin
      aSerNr[aCnt] = CUIVr.SerNr;
      aInvoiceId[aCnt] = CUIVr.InvoiceId;
      aCnt = aCnt + 1;
      if (aCnt >= maxcnt) then begin
        TrHs = false;
      end;
    end;
  end;
  if (aCnt > 0) then begin
    startIndex = 1;
L13:;
    SendSupplierInvoicePdfRequest(EIb,service,partner,(EIb.DebugFlag==1),reply,xdata,aCnt,aInvoiceId,startIndex);
    if (partner == "FITEK3") then begin
    end else begin
      if (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring")) then begin
        tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode");
        errf = true;
        if (LastInRange(tstr,5)=="71") then begin      
          LastChecked = aSerNr[aCnt-1];
        end;
      end;
    end;
    if (errf==false) then begin
      ProcessReceivedIVPdfReply(partner,xdata,EIb.LastInEstEInvStatusSerial,nextAttacmentIndex,aSerNr[0],TotalCnt);
      if (nextAttacmentIndex >= (startIndex + TotalCnt)) then begin
        startIndex = nextAttacmentIndex;
        goto L13;
      end;
      LastChecked = aSerNr[aCnt-1];
    end;
  end;
  return;
end;

global
procedure GetSingleEstEInvoicePdf(record CUIVVc CUIVr,var Boolean errf)
begin
  record ServiceCacheVc SCr;
  record EInvoiceBlock EIb;
  array LongInt aSerNr;
  array string 30 aInvoiceId;
  string 255 tstr,partner;
  Integer aCnt,startIndex,nextAttacmentIndex,TotalCnt;
  Boolean found;
  xml xdata;
  area reply;
  
  SCr.Code = "GETESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    BlockLoad(EIb);
    partner = EIb.EInInvoicePartner;
    aSerNr[0] = CUIVr.SerNr;
    aInvoiceId[0] = CUIVr.InvoiceId;
    aCnt = 1;
    startIndex = 1;
    SendSupplierInvoicePdfRequest(EIb,SCr.Code,partner,(EIb.DebugFlag==1),reply,xdata,aCnt,aInvoiceId,startIndex);
    if (partner == "FITEK3") then begin
    end else begin
      if (XmlNodeExists(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultstring")) then begin
        tstr = XmlGet(xdata,"SOAP-ENV:Envelope/SOAP-ENV:Body/SOAP-ENV:Fault/faultcode");
        errf = true;
      end;
    end;
    if (errf==false) then begin
      ProcessReceivedIVPdfReply(partner,xdata,EIb.LastInEstEInvStatusSerial,nextAttacmentIndex,aSerNr[0],TotalCnt);
    end;
  end;
  return;
end;

global
procedure GetEstEInvoicePdfIdleTask(string arg)
begin
  record ServiceCacheVc SCr;
  Boolean found,testf;
  Integer i,rwcnt,error;
  Integer oldcomp;
  record CompaniesBlock Compb;
  row CompaniesBlock Comprw;
  record EInvoiceBlock EIb;
  LongInt LastChecked;

  SCr.Code = "GETESTEINVOICE";
  found = ReadFirstMain(SCr,1,true);
  if (found) then begin   
    oldcomp = CurrentCompany;  
    BlockLoad(Compb);
    rwcnt = MatRowCnt(Compb);    
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(Compb,i,Comprw);
      if (blank(Comprw.TCPIP)) then begin
        if (SetCompanyCode(Comprw.CompCode,false)) then begin
          BlockLoad(EIb);
          if (EIb.TestFlag == 0) then begin
            if (CheckInternetEnabler==false) then begin
              error = 33506;
              LogText(error,"");
              goto LNextCompanyLine7;
            end;
          end;
          if (EIb.StopInIVEstEInvoices == 1) then begin
            goto LNextCompanyLine7;
          end;
          if (EIb.StopInIVEstEInvPdf == 1) then begin
            goto LNextCompanyLine7;
          end;
          testf = true;
          if (EIb.InIVEstEInvoices==0) then begin 
            testf = false;
          end;
          if (EIb.EInInvoicePartner!="ENVOICE3") then begin//ts - 2018/04, Partner Envoice doesn't support this request
            testf = false;
          end;
          if (testf) then begin
            DoGetEstEInvoicePdf(EIb,SCr.Code,EIb.EInInvoicePartner,LastChecked);
            if (EIb.LastInIVEstEInvSerial <> LastChecked) then begin
              qupdating.UpdateLastInIVEstEInvSerial(EIb,LastChecked);
            end;
          end;
        end;
      end;
LNextCompanyLine7:;
    end;
    ResetCompany(oldcomp);
  end;
LGetEstEInvoicePdfIdleTask:;
  return;
end;
