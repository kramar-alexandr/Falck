// <halrule>server-only</halrule>

external function Integer InString2(string,string);

//new functions only
external procedure SimpleDebugMsg(string, string);

function boolean IsTsekkAuthLink(string link)
begin
  boolean res;

  res = false;
  if (InString2(link,"/?access_validation_bind=") != -1) then begin
    res = true;
  end;
  IsTsekkAuthLink = res;
  return;
end;

function boolean IsTsekkLoginLink(string link)
begin
  boolean res;

  res = false;
  if (InString2(link,"/?code=") != -1) then begin
    res = true;
  end;
  IsTsekkLoginLink = res;
  return;
end;

function string 255 GetRedirectLinkFromArea(area areap)
begin
  string 255 res,startText;
  integer linkStartPos;
  area linkArea;

  res = "";
  startText = "Found. Redirecting to ";
  linkStartPos = FindStringInArea(startText,areap);
  if (linkStartPos != -1) then begin
    linkStartPos = linkStartPos + len(startText);
    res = GetStringFromArea(areap,linkStartPos,GetAreaLength(areap)-linkStartPos);
  end;
  GetRedirectLinkFromArea = res;
  return
end;

global
function boolean RegisterTokenAtTsekk(string partnerName, string token, var area replyArea, var string errMsg)
begin
  boolean res;
  area requestArea;
  record ServiceCacheVc SCr;

  SCr.Code = "LOGINTOTSEKKEE";
  SCr.Partner = "TSEKKEE"; //ts - 2018/04, was TSEKKEELOGIN
  if(ReadFirstMain(SCr,2,true)) then begin
    AddTextToArea("{" & chr(34) & "clientToken" & chr(34) & ":" & chr(34) & token & chr(34) & "}",requestArea);
    SendWebRequest(SCr.ServiceHost,SCr.ServicePort,-1,true,"POST",SCr.FuncName & "/" & partnerName,"application/json; charset=utf-8","",false,requestArea,replyArea,10);
    if (IsTsekkLoginLink(GetRedirectLinkFromArea(replyArea))) then begin
      errMsg = USetStr(1500207);
    end;
    if (IsTsekkAuthLink(GetRedirectLinkFromArea(replyArea))) then begin
      errMsg = USetStr(1500206);
    end;
    res = true;
  end else begin
    SimpleDebugMsg("TSEKK.EE","Login service cache is NOT found.");
  end;
  RegisterTokenAtTsekk = res;
  return;
end;

global
procedure GetLoginLinkRemote(string partnerName, string token,var string res)
begin
  string 255 errMsg;
  area replyArea;
  record EInvoiceBlock EIb;

  if (RegisterTokenAtTsekk(partnerName,token,replyArea,errMsg)) then begin
    res = GetRedirectLinkFromArea(replyArea);
  end else begin
    res = "";
  end;
  BlockLoad(EIb);
  SimpleDebugMsg("TSEKK.EE","Login request sent, check file ./debug/tsekk.ee/loginServerReply.txt for reply details.");
  if(EIb.DebugFlag==1) then begin
    WriteAreaToFile(replyArea,"debug/tsekk.ee/loginServerReply_" & CurrentCompany & ".txt",0);
  end;
  //GetLoginLinkRemote = res;
  return;
end;