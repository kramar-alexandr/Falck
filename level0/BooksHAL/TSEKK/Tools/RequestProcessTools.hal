external updating procedure ProcessReceived2Reply(string,area,string,var date,var time,boolean,boolean,var integer);
external updating procedure AttachFileToVEVI(record VEIVVc,string,Area,boolean,boolean);
//KB 04.12.2017 new functions
remote procedure SimpleDebugMsg(string, string);

function boolean CorrectEInvoiceReply(area reply, xml xmlReply)
begin
  boolean res;

  res = true;
  if(GetAreaLength(reply) <= 0) then begin
    res = false;
    goto LCorrectEInvoiceReply;
  end;
  if (XmlNodeExists(xmlReply,"TsekkRootElement/E_Invoice") == false) then begin
    res = false;
    goto LCorrectEInvoiceReply;
  end;
LCorrectEInvoiceReply:;
  CorrectEInvoiceReply = res;
  return;
end;

procedure AddXMLRootElementToArea(var area targetArea)
begin
  integer firstTagStartPos;
  area xmlArea,rootElemOpenTagArea;
  string 255 startText;

  SetAreaZeroSize(rootElemOpenTagArea);
  SetAreaZeroSize(xmlArea);
  startText = "<E_Invoice>";
  firstTagStartPos = FindStringInArea(startText,targetArea);
  GetAreaFromArea(targetArea,firstTagStartPos,GetAreaLength(targetArea)-firstTagStartPos,xmlArea);
  AddTextToArea("<TsekkRootElement>",rootElemOpenTagArea);
  AddTextToArea("</TsekkRootElement>",xmlArea);
  InsertAreaBeforeArea(rootElemOpenTagArea,xmlArea);
  SetAreaZeroSize(targetArea);
  InsertAreaBeforeArea(xmlArea,targetArea);
  return;
end;

procedure InsertHTTPRequestHeaderREST(var area targetArea)
begin
  record TsekkSetBlock TsekkSetbl;

  BlockLoad(TsekkSetbl);
  if(nonblank(TsekkSetbl.AccessToken)) then begin
    AddTextToArea("ConsumerId: " & TsekkSetbl.AccessToken,targetArea);
    AddTextToArea(chr(13) & chr(10),targetArea);
    AddTextToArea("SubscriberId: hansaworld",targetArea);
    AddTextToArea(chr(13) & chr(10),targetArea);
    AddTextToArea("Accept: application/xml",targetArea);
    AddTextToArea(chr(13) & chr(10),targetArea);
    AddTextToArea(chr(13) & chr(10),targetArea);
  end;
  return;
end;

function string 30 GetFormatedFromDate(date theDate, time theTime)
begin
  string 30 res;
  string 10 timePart,datePart;

  //example "2017-11-15T00:00:00+00:00"
  datePart = DateToString(theDate,"YYYY-MM-DD");
  timePart = theTime & "";
  res = datePart & "T" & timePart;
  GetFormatedFromDate = res;
  return;
end;

global
updating procedure ProcessReceivedPdfTsekk(area reply,var integer cnt)//ts - 2018, added paramater for e-service statistic
begin
  record VEIVVc VEIVr;
  string 255 xkey,regnr,fname;
  LongInt InvoiceID;
  Integer i;
  Boolean testf,TrHs;
  area base64area;
  record EInvoiceBlock EIb;
  xml xdata;

  BlockLoad(EIb);
  if(EIb.AttachPDFtoElectronicInvoice == 0) then begin
    goto LProcessReceivedPdfTsekk;
  end;

  xdata = ParseXmlArea(reply);
  
  i = 0;
  xkey = "TsekkRootElement/E_Invoice/Invoice[" & i & "]/AttachmentFile";
  while (XmlNodeExists(xdata,xkey)) begin
    SimpleDebugMsg("TSEKK.EE","Found attachment " & i);
    testf = false;
    fname = XmlGet(xdata,xkey & "/FileName");
    if (blank(fname)) then begin
      fname = "invoice." & "pdf";
    end;
    invoiceID = StringToLongInt(XmlGetAttribute(xdata,"TsekkRootElement/E_Invoice/Invoice[" & i & "]","invoiceId"));
    if (nonblank(invoiceID)) then begin
      TrHs = true;
      ResetLoop(VEIVr);
      while (LoopMain(VEIVr,1,TrHs)) begin
        if (VEIVr.VESerNr == invoiceID) then begin
          XmlGetArea(xdata,xkey & "/FileBase64",base64area);
          AttachFileToVEVI(VEIVr,fname,base64area,true,false);
          SetAreaZeroSize(base64area);
          SimpleDebugMsg("TSEKK.EE","Saved attachment " & i & " " & fname);
          TrHs = false;
        end;
      end;
    end;
    i = i + 1;
    xkey = "TsekkRootElement/E_Invoice/Invoice[" & i & "]/AttachmentFile";
  end;
  cnt = i;
LProcessReceivedPdfTsekk:;
  return;
end;

function boolean SendRequestToPartnerTsekk(var area reply,var xml xmlReply,string service,string partner,string RequestType,string param,string authphrase,boolean debugMode) 
begin
  record ServiceCacheVc SCr;
  record EInvoiceBlock EIb;
  boolean res;
  string 255 funcName;
  area head,requestArea;
  date curDate;
  time curTime;

  BlockLoad(EIb);
  if(Len(RequestType) > 0) then begin
    SCr.Code = service;
    SCr.Partner = partner;
    if (ReadFirstMain(SCr,2,true)==true) then begin end;
    InsertHTTPRequestHeaderREST(head);
    if(len(authphrase) > 0) then begin
      InsertAreaBeforeArea(head,requestArea);
      if(EIb.AttachPDFtoElectronicInvoice == 1) then begin
        funcName = SCr.FuncName & "/" & RequestType & "?fromDate=" & param;
      end else begin
        funcName = SCr.FuncName & "/" & RequestType & "?fromDate=" & param & "&noAttachment=true";
      end;
      SimpleDebugMsg("TSEKK.EE","Request to: Host:" & SCr.ServiceHost & " Port:" & SCr.ServicePort & " FuncName:" & funcName);
      res = SendRawWebRequest(SCr.ServiceHost,SCr.ServicePort,true,"GET",funcName,"","",false,requestArea,reply,20);
      if(debugMode and GetAreaLength(reply) > 0) then begin
        curDate = CurrentDate;
        curTime = CurrentTime;
        WriteAreaToFile(reply,"debug/tsekk.ee/e-inv_comp" & CurrentCompany & "_" 
        & curDate.day & "_" & curDate.month & "_" & curDate.year & "_" 
        & curTime.hour & "_" & curTime.minute & "_" & curtime.second & ".xml",0);
      end;
      AddXMLRootElementToArea(reply); //to properly distinguish from other e-invoice partners
      xmlReply = ParseXmlArea(reply);
      if(CorrectEInvoiceReply(reply,xmlReply) == false) then begin
        res = false;
        SimpleDebugMsg("TSEKK.EE","Unexpected e-invoice reply received from server, check debug file");
      end;
    end else begin
      SimpleDebugMsg("TSEKK.EE","SendRequestToPartnerTsekk failed, access token is empty");
      res = false;
    end;
  end;
  SendRequestToPartnerTsekk = res;
  return;
end;

global
updating procedure UpdateTsekkeeRequest(record TsekkSetBlock TsekkSetbl,date sdate,time stime)
begin
  TsekkSetbl.LastSuccessReqDate = CurrentDate;
  TsekkSetbl.LastSuccessReqTime = CurrentTime;
  BlockStore(TsekkSetbl);
  return;
end;
  
procedure SendInvoiceRequest(record EInvoiceBlock EIb,string service,string partner,var area reply,var xml xdata)
begin
  record TsekkSetBlock TsekkSetbl;
  string 100 param;
  date SendDate;
  time SendTime;
  
  BlockLoad(TsekkSetbl);
  SendDate = TsekkSetbl.LastSuccessReqDate;
  SendTime = TsekkSetbl.LastSuccessReqTime;
  SetAreaZeroSize(reply);
  if (partner == "TSEKKEE") then begin
    if(blankdate(SendDate) or blanktime(SendTime)) then begin
      SendDate = AddDay(CurrentDate, - GetDay(CurrentDate)+1);
      SendTime.hour = 00;
      SendTime.minute = 00;
      SendTime.second = 00;
      SimpleDebugMsg("TSEKK.EE","SendDate and SendTime are manually set to " & SendDate & " " & SendTime);
    end;
    param = GetFormatedFromDate(SendDate,SendTime);
    if (SendRequestToPartnerTsekk(reply,xdata,service,partner,"receiptsXml",param,TsekkSetbl.AccessToken,(EIb.DebugFlag==1))) then begin
      qupdating.UpdateTsekkeeRequest(TsekkSetbl,CurrentDate,CurrentTime);
    end;
  end;
  return;
end;

global
procedure GetTsekkEInvoice(record EInvoiceBlock EIb,string service,string partner,var integer cnt)//ts - 2018/04, added parameter for e-service statistic
begin
  area reply;
  xml xdata;
  date dummyDate;
  time dummyTime;

  SendInvoiceRequest(EIb,service,partner,reply,xdata);
  qupdating.ProcessReceived2Reply(partner,reply,"",dummyDate,dummyTime,0,0,cnt);//ts - 2018/04, added parameter for automated actions, do not create VIVc from Tsekk.ee transaction
  qupdating.ProcessReceivedPdfTsekk(reply,cnt);
  return;
end;