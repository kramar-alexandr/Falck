//txt tools
external function string 255 AddStr(string,string,string);
remote procedure SendCCRequestToPartner2(string,string,string,var LongInt,var date,var date,var string);
remote function boolean EServiceActivated(string);

global
procedure CollectCodeForContributoryTax(var string tstr,var string code,var string name)
begin
  record TaxVc Taxr;
  Boolean foundf;
  
  Taxr.TaxType = 2;
  foundf = true;
  while (LoopBackKey("TaxType",Taxr,1,foundf)) begin
    if (Taxr.TaxType <> 2) then begin
      foundf = false;
    end;
    if (foundf) then begin
      code = Taxr.Code;
      name = Taxr.Name;
      tstr = AddStr(tstr,Taxr.Code,",");
    end;
  end;
  return;
end;

global
updating procedure SaveEmplRecord(record EmplVc Emplr)
begin
  record EmplVc oldEmplr;

  RecordCopy(oldEmplr,Emplr);
  RecordUpdate(oldEmplr,Emplr,true);
  return;
end;

global
function boolean FillContributoryTaxRow(var record EmplVc Emplr,string tstr,string tcode,string tname,date sdate,date edate,string y_2020,date sd,date ed)
begin
  Boolean res,addrwf;
  row EmplVc Emplrw,newEmplrw;
  Integer i,rwcnt;
  
  rwcnt = MatRowCnt(Emplr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(Emplr,i,Emplrw);
    if nonblank(Emplrw.TaxCode) then begin
      if (SetInSet(Emplrw.TaxCode,tstr)) then begin
        if blankdate(Emplrw.EDate) or (Emplrw.EDate>sdate) then begin
          res = true;
          Emplrw.SDate = sdate;
          Emplrw.EDate = edate;
          if (y_2020=="y") and (blankdate(edate) or (edate>ed)) then begin
            addrwf = true;
            Emplrw.EDate = ed;
          end;
          MatRowPut(Emplr,i,Emplrw);
          if (addrwf) then begin
            ClearRow(Emplr,newEmplrw,1);
            CopyRow(Emplr,Emplrw,newEmplrw);
            newEmplrw.SDate = sd;
            newEmplrw.EDate = edate;            
            MatRowInsert(Emplr,i+1,newEmplrw);
            rwcnt = rwcnt + 1;
          end;
          i = rwcnt;
        end;
      end;
    end;
  end;
  if (res == false) then begin
    ClearRow(Emplr,Emplrw,1);
    Emplrw.TaxCode = tcode;
    Emplrw.TaxName = tname;
    Emplrw.SDate = sdate;
    Emplrw.EDate = edate;
    if (y_2020=="y") and (blankdate(edate) or (edate>ed)) then begin
      addrwf = true;
      Emplrw.EDate = ed;
    end;
    MatRowInsert(Emplr,rwcnt,Emplrw);        
    if (addrwf) then begin
      ClearRow(Emplr,newEmplrw,1);
      CopyRow(Emplr,Emplrw,newEmplrw);
      newEmplrw.SDate = sd;
      newEmplrw.EDate = edate;            
      MatRowInsert(Emplr,rwcnt+1,newEmplrw);
    end;
    res = true;
  end;
  FillContributoryTaxRow = res;
  return;
end;

global
procedure CheckForContributoryContractDsm()
begin
  record EmplVc Emplr;
  string 60 tstr,tcode,tname,y_2020;
  LongInt responsecode;
  date sdate,edate,ed,sd;
  Boolean res;
  
  ed = "30.11.2020"; sd = "01.09.2021";
  GetWindowRecord(CurWindow,Emplr);
  if (Emplr.ClosedFlag!=0) then begin
    goto LCheckForContributoryContractDsm;
  end;
  if (!EServiceActivated("SENDESTCCLOOKUP")) then begin
    MessageBox(1500005,"");
    goto LCheckForContributoryContractDsm;
  end;
  if (len(Emplr.IDCode)!=11) then begin
    MessageBox(1500301,"");      
    goto LCheckForContributoryContractDsm;
  end;
  threadremote.SendCCRequestToPartner2(Emplr.Code,Emplr.IDCode,"SENDESTCCLOOKUP",responsecode,sdate,edate,y_2020);
  switch (responsecode) begin
    case -1: Messagebox(1500600,"");
    case 40520: MessageBox(1500603,"");
    case 40521: MessageBox(1500604,"");
    case 41132: MessageBox(1500602,"");
    case 40552: MessageBox(1500605,"");
    case 0:
      CollectCodeForContributoryTax(tstr,tcode,tname);
      res = FillContributoryTaxRow(Emplr,tstr,tcode,tname,sdate,edate,y_2020,sd,ed);
      if (res) then begin
        PutWindowRecord(CurWindow,Emplr);
        qupdating.SaveEmplRecord(Emplr);
        MessageBox(1500606,"");
      end;
    otherwise
      MessageBox(1500601,"");
  end;
LCheckForContributoryContractDsm:;
  return;
end;
