external procedure PastePPrwsFromCPr(record PaymPayrollVc,var row PaymPayrollVc,record CalcPayrollVc,var integer);
external procedure PPrSumUp(var record PaymPayrollVc);
external function boolean GetNextPaymPayrollSerNr(date,var LongInt);
external function boolean OneEmplOnPaymPayroll(record PaymPayrollVc,var string);
external procedure GetPMBankAccName(string,var string,var string,var string);

function boolean EmplCheck(LongInt contrsernr,string frempl,string toempl,var string emplbankcode,boolean bankcodeflag)
begin
  record ContractVc Contrr;
  record EmplVc Emplr;
  boolean res;

  res = true;
  Contrr.SerNr = contrsernr;
  if (ReadFirstMain(Contrr,1,true)) then begin
    if ((Contrr.EmplCode<frempl) or (Contrr.EmplCode>toempl)) then begin
      res = false;
    end;
    if (bankcodeflag) then begin
      Emplr.Code = Contrr.EmplCode;
      if (ReadFirstMain(Emplr,1,true)) then begin
        emplbankcode = Emplr.BankCode; 
      end;
    end;
  end;
  EmplCheck = res;
  return;
end;

global
updating procedure CreatePaymPayrollMn(record RcVc RepSpec)
begin
  record CalcPayrollVc CPr;
  record PaymPayrollVc PPr;
  row PaymPayrollVc PPrw;
  string 20 frempl,toempl,bankacc,bankname,emplbankcode,bankcode;
  LongInt frcontr,tocontr;
  boolean TrHs,testf,newrecord,grouppaymflag,bankcodeflag,emplcheckflag;
  integer pprwnr;
  string 120 name;

  grouppaymflag = false;
  bankcodeflag = false;
  emplcheckflag = false;
  if (RepSpec.flags[0]!=0) then begin 
    grouppaymflag = true;
  end;  
  if (RepSpec.flags[1]!=0) then begin
    bankcodeflag = true;
  end;

  frempl = FirstInRange(RepSpec.f1,20);
  toempl = LastInRange(RepSpec.f1,20);
  frcontr = FirstInRange(RepSpec.f2,20);
  tocontr = LastInRange(RepSpec.f2,20);  

  newrecord = true;        
  ResetLoop(CPr);
  TrHs = true;
  CPr.CalcDate = RepSpec.sStartDate;
  CPr.ContrSerNr = frcontr;
  CPr.OKFlag = 1;
  CPr.PaymFlag = 0;
  while (LoopKey("CalcDate",CPr,4,TrHs)) begin
    if (DateInRange(CPr.CalcDate,RepSpec.sStartDate,RepSpec.sEndDate)==false) then begin
      TrHs = false;
    end;
    if (TrHs) then begin
      testf = true;
      if ((nonblank(frempl)) or (bankcodeflag)) then begin
        emplcheckflag = EmplCheck(CPr.ContrSerNr,frempl,toempl,emplbankcode,bankcodeflag);
      end;
      if (nonblank(frempl)) then begin
        testf = emplcheckflag;
      end;
      if (bankcodeflag) then begin
        GetPMBankAccName(RepSpec.f3,bankacc,bankname,bankcode);
        if (emplbankcode!=bankcode) then begin
          testf = false;
        end;
      end;
      if ((frcontr>0)) then begin
        if ((CPr.ContrSerNr<frcontr) or (CPr.ContrSerNr>tocontr)) then begin
          testf = false;
        end;
      end;
      if (CPr.OKFlag==0) then begin
        testf = false;
      end;
      if (CPr.PaymFlag!=0) then begin
        testf = false;
      end;
      if (CPr.Invalid!=0) then begin
        testf = false;
      end;
      if (testf) then begin
        if (grouppaymflag==false) then begin
          newrecord = true;        
        end else begin
          if (MatRowCnt(CPr)>(99-pprwnr)) then begin
            if (RecordInsert(PPr,true)) then begin end; 
            newrecord = true;        
          end;
        end;
        if (newrecord) then begin
          pprwnr = 0;
          RecordNew(PPr);
          PPr.SerNr = -1;
          if blankdate(RepSpec.d1) then begin
            PPr.PaymDate = CurrentDate;
          end else begin
            PPr.PaymDate = RepSpec.d1;
          end;          
          PPr.PMCode = RepSpec.f3;
          if (GetNextPaymPayrollSerNr(PPr.PaymDate,PPr.SerNr)) then begin end;
          if (grouppaymflag) then begin
            newrecord = false;
         /* //ts 2018/09, Ÿhel vŠljamaksel v›ib olla mitu arvestust
          end else begin
            PPr.ObjCode=CPr.ObjCode;
          */
          end;
        end;
        PastePPrwsFromCPr(PPr,PPrw,CPr,pprwnr);
        PPrSumUp(PPr);
        if (OneEmplOnPaymPayroll(PPr,name)) then begin
          PPr.Name = name;
        end else begin
          PPr.Name = "";
        end;
        if (grouppaymflag) then begin
          if (pprwnr==99) then begin
            if (RecordInsert(PPr,true)) then begin end; 
            newrecord = true;
          end;
        end else begin
          if (pprwnr>0) then begin  
            if (RecordInsert(PPr,true)) then begin end; 
          end;
        end;
      end;
    end;
  end;
  if (grouppaymflag) then begin
    if (pprwnr>0) and (pprwnr<99) then begin
      if (RecordInsert(PPr,true)) then begin end; 
    end;
  end;          
LCreatePaymPayrollMn:;
  return;
end;
